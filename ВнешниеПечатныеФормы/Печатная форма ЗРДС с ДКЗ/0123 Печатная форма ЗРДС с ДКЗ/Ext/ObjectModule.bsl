
Функция СведенияОВнешнейОбработке() Экспорт     
	
	ПараметрыРегистрации 				= ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.3.3.0");
	ПараметрыРегистрации.Информация 	= НСтр("ru = 'Заявка на оплату'"); 
	ПараметрыРегистрации.Наименование 	= НСтр("ru = 'Печатная форма документа Заявка На Оплату'");
	ПараметрыРегистрации.Вид 			= ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма(); 
	// <ИГС> ПР Потылицын Г.С. № 00-4210 14.01.2025 {
	ПараметрыРегистрации.Версия 		= "1.3";
	// } </ИГС>
	ПараметрыРегистрации.Назначение.Добавить("Документ.игсЗаявкаНаОплату");
	
	Команда 							= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление 				= НСтр("ru = 'Заявка на оплату'");
	Команда.Идентификатор 				= "ЗаявкаНаОплату";
	Команда.Использование 				= ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение 		= Истина;
	Команда.Модификатор 				= "ПечатьMXL";
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ЗаявкаНаОплату");
	
	Если ПечатнаяФорма <> Неопределено Тогда  
		
		ПечатнаяФорма.ТабличныйДокумент = СоздатьТабличныйДокументЗРДС(МассивОбъектов);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'ЗРДС'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Объект.Макеты.ЗРДС"; 
		
	КонецЕсли;
	
КонецПроцедуры  

Функция СоздатьТабличныйДокументЗРДС(МассивОбъектов)
	
	ТабДокумент							= Новый ТабличныйДокумент;  
	ТабДокумент.АвтоМасштаб 			= Истина; 
	ТабДокумент.КлючПараметровПечати 	= "ЗАЯВКАНАОПЛАТУ";
	
	Счет62 								= ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками;
	Макет 								= ПолучитьМакет("ЗРДС");
	
	Запрос = Новый Запрос;                     
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	игсЗаявкаНаОплату.Ссылка КАК Ссылка,
	|	игсЗаявкаНаОплату.Дата КАК Дата,
	|	игсЗаявкаНаОплату.Номер КАК Номер,
	|	игсЗаявкаНаОплату.ВидОперации КАК ВидОперации,
	|	игсЗаявкаНаОплату.Комментарий КАК Комментарий,
	|	игсЗаявкаНаОплату.Организация КАК Организация,
	|	игсЗаявкаНаОплату.ЦФО КАК ЦФО,
	|	игсЗаявкаНаОплату.Инициатор КАК Инициатор,
	|	игсЗаявкаНаОплату.СуммаПлатежа КАК СуммаКОплате,
	|	игсЗаявкаНаОплату.СуммаНДС КАК НДС,
	|	игсЗаявкаНаОплату.НДССверху КАК НДССверху,
	|	игсЗаявкаНаОплату.СтавкаНДС КАК СтавкаНДС,
	|	игсЗаявкаНаОплату.ВидОплаты КАК ВидОплаты,
	|	игсЗаявкаНаОплату.Контрагент.НаименованиеПолное КАК Контрагент,
	|	игсЗаявкаНаОплату.ВалютаДокумента КАК ВалютаДокумента,
	|	игсЗаявкаНаОплату.СчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	игсЗаявкаНаОплату.НазначениеПлатежа КАК НазначениеИспользования,
	|	игсЗаявкаНаОплату.Проект КАК ПроектМероприятие,
	|	игсЗаявкаНаОплату.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	игсЗаявкаНаОплату.СуммаПлатежа КАК СуммаПлатежа,
	|	игсЗаявкаНаОплату.СрокОплаты КАК ДатаОплаты,
	|	игсЗаявкаНаОплату.Контрагент.КПП КАК КПП,
	|	игсЗаявкаНаОплату.Контрагент.ИНН КАК ИНН,
	|	игсЗаявкаНаОплату.СчетКонтрагента.НомерСчета КАК СчетПолучателя,
	|	игсЗаявкаНаОплату.СчетКонтрагента.Банк КАК БанкПолучателя,
	|	игсЗаявкаНаОплату.СчетКонтрагента.Банк.Город КАК Город,
	|	игсЗаявкаНаОплату.СчетКонтрагента.Банк.КоррСчет КАК КоррСчет,
	|	игсЗаявкаНаОплату.СчетКонтрагента.Банк.Код КАК БИК,
	|	игсЗаявкаНаОплату.СуммаДокумента КАК СуммаДокумента,
	|	игсЗаявкаНаОплату.СчетКонтрагента.БанкДляРасчетов КАК КоррБанк,
	|	игсЗаявкаНаОплату.СчетКонтрагента.БанкДляРасчетов.КоррСчет КАК БанковскийСчетКонтрагентаБанкДляРасчетовКоррСчет,
	|	игсЗаявкаНаОплату.СчетКонтрагента.БанкДляРасчетов.Адрес КАК КоррБанкАдрес,
	|	игсЗаявкаНаОплату.СчетКонтрагента.Банк.Адрес КАК АдресБанка,
	|	игсЗаявкаНаОплату.КурсНаДатуЗРДС КАК КурсНаДатуЗРДС,
	|	игсЗаявкаНаОплату.Приложение1 КАК Приложение1,
	|	игсЗаявкаНаОплату.Приложение2 КАК Приложение2,
	|	игсЗаявкаНаОплату.Приложение3 КАК Приложение3,
	|	игсЗаявкаНаОплату.ВидЗатрат КАК ВидЗатрат,
	|	игсЗаявкаНаОплату.СтатусОплатыЗаказчиком КАК СтатусОплатыЗаказчиком,
	|	игсЗаявкаНаОплату.Контрагент.ЮридическоеФизическоеЛицо КАК ЮрФизЛицо,
	|	игсЗаявкаНаОплату.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
	|	игсЗаявкаНаОплату.Проект.Представление КАК ПроектПредставление,
	|	игсЗаявкаНаОплату.ВалютаПлатежа.Наименование КАК ВалютаПлатежаНаименование,
	|	игсЗаявкаНаОплату.ВалютаПлатежа.ПараметрыПрописи КАК ВалютаПлатежаПараметрыПрописи,
	|	игсЗаявкаНаОплату.СчетКонтрагента.ТекстКорреспондента КАК БанковскийСчетКонтрагентаТекстКорреспондента,
	|	игсЗаявкаНаОплату.СтатусСоставителя КАК Статус,
	|	игсЗаявкаНаОплату.КодБК КАК КБК,
	|	игсЗаявкаНаОплату.КодОКАТО КАК ОКАТО,
	|	игсЗаявкаНаОплату.ПоказательНомера КАК НомерНалДок,
	|	игсЗаявкаНаОплату.ПоказательДаты КАК ДатаНалДок,
	|	игсЗаявкаНаОплату.ПоказательОснования КАК ОсновнойНП,
	|	игсЗаявкаНаОплату.ПоказательТипа КАК ТипНалДок,
	|	игсЗаявкаНаОплату.ПоказательПериода КАК НалоговыйПериод,
	|	игсЗаявкаНаОплату.КПП КАК КППОрганизации,
	|	игсЗаявкаНаОплату.СтатьяДвиженияДенежныхСредств.Представление КАК СтатьяДДСПредставление
	|ИЗ
	|	Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
	|ГДЕ
	|	игсЗаявкаНаОплату.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат 		= Запрос.Выполнить();
	ВыборкаПоШапке 	= Результат.Выбрать(); 
	
	Пока ВыборкаПоШапке.Следующий() Цикл
		
		ОбластьНалоги = Макет.ПолучитьОбласть("Налоги");
		
		ПараметрыДляТаблицыОплат = Новый Структура;
		ПараметрыДляТаблицыОплат.Вставить("ДатаЗаявки", 	ВыборкаПоШапке.Дата);
		ПараметрыДляТаблицыОплат.Вставить("Организация", 	ВыборкаПоШапке.Организация);
		
		ТаблицаОплат = ПолучитьТаблицуОплат(ВыборкаПоШапке.Ссылка, ПараметрыДляТаблицыОплат);
		
		Документ = ВыборкаПоШапке.Ссылка;
		
		ЭтоПодотчет = ПолучитьПодОтчетЗаявки(ВыборкаПоШапке.СтатьяДДС);
		
		Если ВыборкаПоШапке.ЮрФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ЭтоПодотчет = Ложь;
		КонецЕсли; 
		
		Если ВыборкаПоШапке.ВидЗатрат = Перечисления.игсВидЗатратЗаявкиНаОплату.ОсновноеПроизводство Тогда
			Шапка = Макет.ПолучитьОбласть("Шапка");
			ШапкаБезПроекта = Ложь;
		ИначеЕсли ВыборкаПоШапке.ВидЗатрат = Перечисления.игсВидЗатратЗаявкиНаОплату.CAPEX Тогда
			Шапка = Макет.ПолучитьОбласть("ШапкаCAPEX");
			ШапкаБезПроекта = Истина;
		ИначеЕсли ЭтоПодотчет Тогда
			Шапка = Макет.ПолучитьОбласть("ШапкаПодотчет");
			ШапкаБезПроекта = Истина;
		Иначе
			Шапка = Макет.ПолучитьОбласть("ШапкаБезПроекта");
			ШапкаБезПроекта = Истина;
		КонецЕсли;
		
		ДетализироватьСтатусОплаты 	= Ложь;
		Категория 					= "";
		КатегорияЧисло 				= 0;  
		
		Если ТаблицаОплат <> Неопределено Тогда
			
			Если ВыборкаПоШапке.ВидЗатрат = Перечисления.игсВидЗатратЗаявкиНаОплату.ОсновноеПроизводство Тогда
				   
				ЧастичнаяОплата = Ложь;  
				
				Для Каждого СтрокаОплаты Из ТаблицаОплат Цикл  
					 
					Если СтрокаОплаты.ЗРДС = ВыборкаПоШапке.Ссылка Тогда 
						
						Если СтрокаОплаты.КатегорияЧисло = 1 Тогда
							ЧастичнаяОплата = Истина;	
						КонецЕсли;      
						
						Если СтрокаОплаты.КатегорияЧисло > КатегорияЧисло Тогда   
							
							Категория 		= СтрокаОплаты.Категория;
							КатегорияЧисло  = СтрокаОплаты.КатегорияЧисло; 
							
						КонецЕсли;
						
					КонецЕсли; 
					
				КонецЦикла;   
				
				Если КатегорияЧисло = 4 Тогда  
					
					Шапка 			= Макет.ПолучитьОбласть("Шапка2|Область1");
					ШапкаОбласть2   = Макет.ПолучитьОбласть("Шапка2|Область2"); 
					
					ДетализироватьСтатусОплаты 	= Истина;
					ШапкаБезПроекта 			= Ложь;
					
				КонецЕсли;     
				
				Если КатегорияЧисло = 4
					И ЧастичнаяОплата Тогда 
					
					Категория = "Оплата заказчиком произведена частично";
					
				КонецЕсли; 
				
				Шапка.Параметры.Категория = Категория;
				
				Если НЕ ЗначениеЗаполнено(Категория) 
					И ЗначениеЗаполнено(ВыборкаПоШапке.СтатусОплатыЗаказчиком) Тогда
					
					Если ВыборкаПоШапке.СтатусОплатыЗаказчиком = Перечисления.игсСтатусыОплатыЗаказчиком.ОплатаЗаказчикомНеПроизведена Тогда 
						
						Шапка 			= Макет.ПолучитьОбласть("Шапка2|Область1");
						ШапкаОбласть2   = Макет.ПолучитьОбласть("Шапка2|Область2");
						
						ДетализироватьСтатусОплаты 	= Истина;   
						ШапкаБезПроекта 			= Ложь;
						
					КонецЕсли;             
					
					Шапка.Параметры.Категория = ВыборкаПоШапке.СтатусОплатыЗаказчиком;
					
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЕсли; 
		
		Шапка.Параметры.Заполнить(ВыборкаПоШапке);  
		
		//Вид затрат
		ТекСтатья 		= ВыборкаПоШапке.СтатьяДДС;
		ТекРодитель 	= ТекСтатья.Родитель; 
		
		Пока НЕ ТекРодитель = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка() Цикл 
			
			ТекСтатья	= ТекРодитель;
			ТекРодитель	= ТекСтатья.Родитель; 
			
		КонецЦикла;
		
		ФормСтрока 		= "Л = ru_RU; ДП = Истина";
		
		ПарПредмета 	= ВыборкаПоШапке.ВалютаПлатежаПараметрыПрописи;
		ПрописьЧисла 	= ЧислоПрописью(ВыборкаПоШапке.СуммаПлатежа, ФормСтрока, ПарПредмета);
		
		Если ДетализироватьСтатусОплаты Тогда 
			
			ШапкаОбласть2.Параметры.СуммаКОплате 	= Формат(ВыборкаПоШапке.СуммаПлатежа,"ЧДЦ=2") + " " + ВыборкаПоШапке.ВалютаПлатежаНаименование;
			ШапкаОбласть2.Параметры.СуммаПрописью 	= "("+ПрописьЧисла+")"; 
			
			Если ВыборкаПоШапке.НДС>0 Тогда 
				
				СуммаНДС 					= ВыборкаПоШапке.НДС;
				ШапкаОбласть2.Параметры.НДС = Формат(СуммаНДС,"ЧДЦ=2") + " " + ВыборкаПоШапке.ВалютаПлатежаНаименование;
				
			Иначе  
				
				ШапкаОбласть2.Параметры.НДС = "Без НДС"; 
				
			КонецЕсли; 
			
			ШапкаОбласть2.Параметры.ПечНДС 	= ВыборкаПоШапке.СтавкаНДС; 
			ШапкаОбласть2.Параметры.КопияОригинал = ПолучитьКопиюОригиналДокумента(Документ);  
			
		Иначе            
			
			Шапка.Параметры.СуммаКОплате 	= Формат(ВыборкаПоШапке.СуммаПлатежа,"ЧДЦ=2") + " " + ВыборкаПоШапке.ВалютаПлатежаНаименование;
			Шапка.Параметры.СуммаПрописью 	= "("+ПрописьЧисла+")"; 
			
			Если ВыборкаПоШапке.НДС>0 Тогда   
				
				СуммаНДС 			= ВыборкаПоШапке.НДС;
				Шапка.Параметры.НДС = Формат(СуммаНДС,"ЧДЦ=2") + " " + ВыборкаПоШапке.ВалютаПлатежаНаименование; 
				
			Иначе  
				
				Шапка.Параметры.НДС = "Без НДС";
				
			КонецЕсли; 
			
			Шапка.Параметры.ПечНДС 	= ВыборкаПоШапке.СтавкаНДС;
			Шапка.Параметры.КопияОригинал = ПолучитьКопиюОригиналДокумента(Документ);  
			
		КонецЕсли; 
		
		Шапка.Параметры.ДатаОплаты 	= Формат(ВыборкаПоШапке.ДатаОплаты,"ДФ=dd.MM.yyyy");
		Шапка.Параметры.Дата		= Формат(ВыборкаПоШапке.Дата,"ДФ=dd.MM.yyyy");
		Договор 					= ВыборкаПоШапке.ДоговорКонтрагента;  
		
		Если Не ЭтоПодотчет Тогда      
			
			ИнформацияДоговора = ?(ЗначениеЗаполнено(Договор),
			?(ЗначениеЗаполнено(Договор.ДоговорПодписан),
			СокрЛП(Договор.Наименование), 
			СокрЛП(Договор.Наименование) + Символы.ПС + " (ДОГОВОР НЕ ПОДПИСАН)"), 
			"ДОГОВОР НЕ УКАЗАН");
			
			Если Договор.игсРамочныйДоговор Тогда
				ИнформацияДоговора = ИнформацияДоговора + Символы.ПС + "(РАМОЧНЫЙ ДОГОВОР)";
			КонецЕсли; 
			
			Если Договор.ДоговорЗакрыт Тогда
				ИнформацияДоговора = ИнформацияДоговора + СИмВОЛЫ.ПС + "(ДОГОВОР ЗАКРЫТ)";
			КонецЕсли; 
			
			Шапка.Параметры.ИнформацияДоговора = ИнформацияДоговора; 
			
		КонецЕсли;
		
		ТабДокумент.Вывести(Шапка); 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Организация КАК Организация,
		|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстатокДт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет В ИЕРАРХИИ(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками)),
		|			&СубконтоКонтрагентДоговор,
		|			Субконто1 = &Контрагент
		|				И Субконто2 = &ДоговорКонтрагента) КАК ХозрасчетныйОстатки"; 
		
		Запрос.УстановитьПараметр("Период", КонецДня(ВыборкаПоШапке.Дата));
		Запрос.УстановитьПараметр("Контрагент", ВыборкаПоШапке.Контрагент); 
		
		СубконтоРегистра = Новый Массив;
		СубконтоРегистра.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		СубконтоРегистра.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
		
		Запрос.УстановитьПараметр("СубконтоКонтрагентДоговор", 	СубконтоРегистра);
		Запрос.УстановитьПараметр("ДоговорКонтрагента", 		ВыборкаПоШапке.ДоговорКонтрагента); 
		Запрос.УстановитьПараметр("Счет62", 					Счет62);  
		
		Результат 	= Запрос.Выполнить();
		ЕстьКЗ 		= НЕ Результат.Пустой();
		
		Если ЕстьКЗ 
			И ВыборкаПоШапке.ВидОплаты = Перечисления.игсВидыОплатЗРДС.Предоплата Тогда
			
			ОбластьШапкаКЗ = Макет.ПолучитьОбласть("ШапкаКЗ"); 
			
			ТабДокумент.Вывести(ОбластьШапкаКЗ);
			
		КонецЕсли;   
		
		Если ЕстьКЗ Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ХозрасчетныйОбороты.Организация КАК Организация,
			|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
			|	ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаОборотДт
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Обороты(
			|			&НачалоПериода,
			|			&КонецПериода,
			|			Регистратор,
			|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками)),
			|			&СубконтоКонтрагентДоговор,
			|			Субконто1 = &Контрагент
			|				И Субконто2 = &ДоговорКонтрагента,
			|			,
			|			) КАК ХозрасчетныйОбороты";   
			
			Запрос.УстановитьПараметр("Счет62", 			Счет62);
			Запрос.УстановитьПараметр("НачалоПериода", 		НачалоДня(ВыборкаПоШапке.Дата) - 3600*24*30);
			Запрос.УстановитьПараметр("КонецПериода", 		Новый Граница(КонецДня(ВыборкаПоШапке.Дата), ВидГраницы.Включая));
			Запрос.УстановитьПараметр("Контрагент", 		ВыборкаПоШапке.Контрагент);
			Запрос.УстановитьПараметр("ДоговорКонтрагента", ВыборкаПоШапке.ДоговорКонтрагента); 
			
			Результат = Запрос.Выполнить();
			
			Если Результат.Пустой() Тогда 
				
				ОбластьШапкаБолее30 = Макет.ПолучитьОбласть("ШапкаЗадолженностьБолее30");  
				ТабДокумент.Вывести(ОбластьШапкаБолее30); 
				
			КонецЕсли; 
			
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Документ.Контрагент) Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст =  
			"ВЫБРАТЬ
				|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
				|ИЗ
				|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет = &Счет6201, &СубконтоСчета, Субконто1 = &Контрагент) КАК ХозрасчетныйОстатки";
			
			Запрос.УстановитьПараметр("КонецПериода", Новый Граница(КонецДня(Документ.Дата), ВидГраницы.Включая));
			Запрос.УстановитьПараметр("Контрагент", Документ.Контрагент); 
			Запрос.УстановитьПараметр("Счет6201", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
			
			СубконтоСчета = Новый Массив;
			СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
			СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
			
			Запрос.УстановитьПараметр("СубконтоСчета", СубконтоСчета);
			
			Результат = Запрос.Выполнить();  
			
			Если Не Результат.Пустой() Тогда
				
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				
				Если ЗначениеЗаполнено(Выборка.Сумма) Тогда
					
					ОбластьШапкаДЗ = Макет.ПолучитьОбласть("ШапкаДЗ");
					ОбластьШапкаДЗ.Параметры.Сумма = Формат(-Выборка.Сумма, "ЧЦ=15; ЧДЦ=2");
					ТабДокумент.Вывести(ОбластьШапкаДЗ);
					
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЕсли;
		
		Если ДетализироватьСтатусОплаты Тогда  
			
			Если КатегорияЧисло = 4 Или КатегорияЧисло = 1 Тогда
				
				ШапкаОплата	= Макет.ПолучитьОбласть("ШапкаОплата"); 
				
				ТабДокумент.Вывести(ШапкаОплата);
				
				СтрокаОплата 			= Макет.ПолучитьОбласть("СтрокаОплата");
				СтрокаОплатаНеОплачено 	= Макет.ПолучитьОбласть("СтрокаОплатаНеОплачено");
				
				ТаблицаОплат.Свернуть("НоменклатурнаяГруппа, Категория, КатегорияЧисло, Сумма", "");
				
				Для Каждого СтрокаОплаты Из ТаблицаОплат Цикл 
					
					Если СтрокаОплаты.ЗРДС = ВыборкаПоШапке.Ссылка Тогда 
						
						СтрокаОплатаНаПечать = ?(СтрокаОплаты.КатегорияЧисло = 4, СтрокаОплатаНеОплачено, СтрокаОплата);
						СтрокаОплатаНаПечать.Параметры.Заполнить(СтрокаОплаты);  
						
						ТабДокумент.Вывести(СтрокаОплатаНаПечать);  
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли; 
			
			ТабДокумент.Вывести(ШапкаОбласть2);
			
		КонецЕсли; 
		
		Реквизиты = Макет.ПолучитьОбласть("Реквизиты");
		Реквизиты.Параметры.Заполнить(ВыборкаПоШапке);
		
		Реквизиты.Параметры.Контрагент = ?(ЗначениеЗаполнено(ВыборкаПоШапке.БанковскийСчетКонтрагентаТекстКорреспондента), ВыборкаПоШапке.БанковскийСчетКонтрагентаТекстКорреспондента,ВыборкаПоШапке.Контрагент);
		
		ТабДокумент.Вывести(Реквизиты);
		
		Если ВыборкаПоШапке.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога Тогда
			
			ОбластьНалоги.Параметры.Заполнить(ВыборкаПоШапке);
			ОбластьНалоги.Параметры.ДатаНалДок = Формат(ОбластьНалоги.Параметры.ДатаНалДок,"ДФ=dd.MM.yyyy"); 
			
			ТабДокумент.Вывести(ОбластьНалоги);
			
		КонецЕсли;
		
		ОтветственныйЗаДокумент 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ,"Ответственный");
		ПредставлениеФИО 			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтветственныйЗаДокумент,"Представление"); 
		
		Отвественность 				= Макет.ПолучитьОбласть("Отвественность");
		
		Отвественность.Параметры.ОтветственныйИсполнитель  			= ПредставлениеФИО;
		Отвественность.Параметры.ОтветственныйИсполнительТекст 		= "Подтверждаю необходимость оплаты выбранному контрагенту по указанным реквизитам. Документы платежу соответствуют.";	
		Отвественность.Параметры.РуководительПроектаТекст 			= "Подтверждаю необходимость оплаты выбранному контрагенту.";	
		Отвественность.Параметры.ГлавныйБухгалтерТекст 				= "Бухгалтерская и налоговая экспертиза Заявки на оплату и первичной документации проведены. Замечаний нет.";	
		Отвественность.Параметры.ФинансовыйДиректорТекст 			= "Согласовываю проведение платежа.";
		Отвественность.Параметры.РуководительПроектаДолжность 		= "Руководитель проекта";
		Отвественность.Параметры.ОтветственныйИсполнительДолжность 	= "Ответственный исполнитель";
		Отвественность.Параметры.ГлавныйБухгалтерДолжность 			= "Главный бухгалтер";
		Отвественность.Параметры.ФинансовыйДиректорДолжность 		= "Финансовый директор";
		
		Приложения = Макет.ПолучитьОбласть("Приложения");	
		Приложения.Параметры.Приложение1 = ВыборкаПоШапке.Приложение1;	
		Приложения.Параметры.Приложение2 = ВыборкаПоШапке.Приложение2;	
		Приложения.Параметры.Приложение3 = ВыборкаПоШапке.Приложение3;
		
		ТабДокумент.Вывести(Приложения);
		ТабДокумент.Вывести(Отвественность);
		
		ТабДокумент.ОтображатьСетку 	= Ложь;
		ТабДокумент.ОтображатьЗаголовки = Ложь;
		ТабДокумент.ТолькоПросмотр 		= Истина;
		
		
		Если НЕ Документ.СтатьяДвиженияДенежныхСредств.ПринадлежитЭлементу(Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Налоги", Истина)) 
			И НЕ Документ.СтатьяДвиженияДенежныхСредств.ПринадлежитЭлементу(Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Налоги и сборы (с ФОТ)", Истина)) Тогда 
			
			ВывестиДКЗ(ТабДокумент, Макет, Документ);	
			
		КонецЕсли;	
		
		Если Документ.СтатьяДвиженияДенежныхСредств.ПринадлежитЭлементу(Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоНаименованию("Аренда ОС", Истина)) Тогда 
			
			ВывестиОС(ТабДокумент, Макет, Документ); 
			
		КонецЕсли;
		
		ВывестиКопииОригиналы(ТабДокумент, Макет, Документ);
		ВывестиОплатуПоСчету(ТабДокумент, Макет, Документ);
		ВывестиВсеАвансыПоДоговору(ТабДокумент, Макет, Документ);
		ВывестиРанееПроведенныеОплаты(ТабДокумент, Макет, Документ); 
		
		Если МассивОбъектов.Количество() > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); 
		КонецЕсли; 
		
	КонецЦикла;    
	
	Возврат ТабДокумент;
	
КонецФункции	

Процедура ВывестиВсеАвансыПоДоговору(ТабДокумент, Макет, Документ) 
	
	Если Документ.ВидОплаты = Перечисления.игсВидыОплатЗРДС.Предоплата Тогда
		
		ДоговорКонтрагента  = Документ.ДоговорКонтрагента;
		Контрагент			= Документ.Контрагент;
		Дата				= Документ.Дата;
		
		Шапка 		= Макет.ПолучитьОбласть("ШапкаАвансыПодоговору");
		Строка		= Макет.ПолучитьОбласть("СтрокаАвансыПоДоговору");
		Итоги		= Макет.ПолучитьОбласть("ИтогиАвансыПоДоговору");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсЗаявкаНаОплату.Ссылка КАК Ссылка,
		|	игсЗаявкаНаОплату.НазначениеПлатежа,
		|	игсЗаявкаНаОплату.СуммаДокумента
		|ИЗ
		|	Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
		|ГДЕ
		|	игсЗаявкаНаОплату.Проведен
		|	И игсЗаявкаНаОплату.ВидОплаты = ЗНАЧЕНИЕ(Перечисление.игсВидыОплатЗРДС.Предоплата)
		|	И игсЗаявкаНаОплату.Контрагент = &Контрагент
		|	И игсЗаявкаНаОплату.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И игсЗаявкаНаОплату.Дата <= &Дата
		|	И игсЗаявкаНаОплату.Ссылка <> &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ"; 
		
		Запрос.УстановитьПараметр("Контрагент", 		Контрагент);
		Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
		Запрос.УстановитьПараметр("Ссылка", 			Документ);
		Запрос.УстановитьПараметр("Дата", 				КонецДня(Дата));
		
		Результат 	= Запрос.Выполнить();
		Выборка 	= Результат.Выбрать();
		
		Если Не Результат.Пустой() Тогда
			
			Шапка.Параметры.ДоговорКонтрагента = Строка(ДоговорКонтрагента);
			ТабДокумент.Вывести(Шапка);
			
			СуммаДокумента = 0;  
			
			Пока Выборка.Следующий() Цикл
				
				Строка.Параметры.Заполнить(Выборка);
				
				ТабДокумент.Вывести(Строка);   
				
				СуммаДокумента = СуммаДокумента + Выборка.СуммаДокумента;
				
			КонецЦикла;
			
			Итоги.Параметры.СуммаДокумента = СуммаДокумента;
			
			ТабДокумент.Вывести(Итоги);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ВывестиДКЗ(ТабДокумент, Макет, Документ)
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	|	ХозрасчетныйОстаткиИОбороты.Счет КАК СчетОплаты,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК ДоговорКонтрагента,
	|	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Документ,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК РегПриход,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК РегРасход,
	|	0 КАК РегПриходВал,
	|	0 КАК РегРасходВал,
	|	ХозрасчетныйОстаткиИОбороты.Регистратор КАК Регистратор,
	|	СуммаНачальныйОстатокДт КАК РегНачОстатокДт,
	|	СуммаНачальныйОстатокКт КАК РегНачОстатокКт,
	|	СуммаКонечныйОстатокДт КАК РегКонОстатокДт,
	|	СуммаКонечныйОстатокКт КАК РегКонОстатокКт,
	|	ВалютнаяСуммаНачальныйОстатокДт КАК РегНачОстатокДтВал,
	|	ВалютнаяСуммаНачальныйОстатокКт КАК РегНачОстатокКтВал,
	|	ВалютнаяСуммаКонечныйОстатокДт КАК РегКонОстатокДтВал,
	|	ВалютнаяСуммаКонечныйОстатокКт КАК РегКонОстатокКтВал
	|ПОМЕСТИТЬ ВТ_ЗАчетАванса
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|			&Начало,
	|			&Конец,
	|			Регистратор,
	|			,
	|			Счет В ИЕРАРХИИ (&Счета_60_62),
	|			,
	|			Организация = &Организация
	|				И Субконто1 = &Контрагент) КАК ХозрасчетныйОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураПолученный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураПолученный.Ссылка КАК Ссылка,
	|	СчетФактураПолученный.Контрагент КАК Контрагент,
	|	СчетФактураПолученный.ДатаВходящегоДокумента КАК ДатаСФ,
	|	СчетФактураПолученный.НомерВходящегоДокумента КАК НомерСФ
	|ПОМЕСТИТЬ ТаблСчФ
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.Проведен
	|	И (СчетФактураПолученный.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ИЛИ СчетФактураПолученный.ДокументОснование ССЫЛКА Документ.ПоступлениеДопРасходов)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураПолученный.ДокументОснование,
	|	СчетФактураПолученный.Контрагент,
	|	СчетФактураПолученный.ДатаВходящегоДокумента,
	|	СчетФактураПолученный.НомерВходящегоДокумента,
	|	СчетФактураПолученный.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование,
	|	СчетФактураПолученныйДокументыОснования.Ссылка,
	|	СчетФактураПолученный.Контрагент,
	|	СчетФактураПолученный.ДатаВходящегоДокумента,
	|	СчетФактураПолученный.НомерВходящегоДокумента
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ПО СчетФактураПолученныйДокументыОснования.Ссылка = СчетФактураПолученный.Ссылка
	|			И СчетФактураПолученныйДокументыОснования.ИсходныйДокумент = СчетФактураПолученный.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.Контрагент,
	|	СчетФактураВыданный.Дата,
	|	СчетФактураВыданный.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗАчетАванса.Организация КАК Организация,
	|	ВТ_ЗАчетАванса.СчетОплаты КАК СчетОплаты,
	|	ВТ_ЗАчетАванса.Контрагент КАК Контрагент,
	|	ВТ_ЗАчетАванса.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ЗАчетАванса.Документ КАК Документ,
	|	СУММА(ВТ_ЗАчетАванса.РегНачОстатокДт) КАК РегНачОстатокДт,
	|	СУММА(ВТ_ЗАчетАванса.РегНачОстатокКт) КАК РегНачОстатокКт,
	|	СУММА(ВТ_ЗАчетАванса.РегПриход) КАК РегПриход,
	|	СУММА(ВТ_ЗАчетАванса.РегРасход) КАК РегРасход,
	|	СУММА(ВТ_ЗАчетАванса.РегКонОстатокДт) КАК РегКонОстатокДт,
	|	СУММА(ВТ_ЗАчетАванса.РегКонОстатокКт) КАК РегКонОстатокКт,
	|	СУММА(ВТ_ЗАчетАванса.РегПриходВал) КАК РегПриходВал,
	|	СУММА(ВТ_ЗАчетАванса.РегРасходВал) КАК РегРасходВал,
	|	СУММА(ВТ_ЗАчетАванса.РегКонОстатокДтВал) КАК РегКонОстатокДтВал,
	|	СУММА(ВТ_ЗАчетАванса.РегКонОстатокКтВал) КАК РегКонОстатокКтВал,
	|	СУММА(ВТ_ЗАчетАванса.РегНачОстатокДтВал) КАК РегНачОстатокДтВал,
	|	СУММА(ВТ_ЗАчетАванса.РегНачОстатокКтВал) КАК РегНачОстатокКтВал,
	|	ВТ_СФ.ДатаСФ КАК ДатаСФ,
	|	ВТ_СФ.НомерСФ КАК НомерСФ,
	|	ДокументПТиУ.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ДокументПТиУ.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента
	|ПОМЕСТИТЬ ВТДанные
	|ИЗ
	|	ВТ_ЗАчетАванса КАК ВТ_ЗАчетАванса
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблСчФ КАК ВТ_СФ
	|		ПО ВТ_ЗАчетАванса.Документ = ВТ_СФ.ДокументОснование
	|			И ВТ_ЗАчетАванса.Контрагент = ВТ_СФ.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ДокументПТиУ
	|		ПО ВТ_ЗАчетАванса.Документ = ДокументПТиУ.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЗАчетАванса.ДоговорКонтрагента,
	|	ВТ_ЗАчетАванса.Документ,
	|	ВТ_ЗАчетАванса.Контрагент,
	|	ВТ_ЗАчетАванса.Организация,
	|	ВТ_СФ.ДатаСФ,
	|	ВТ_СФ.НомерСФ,
	|	ДокументПТиУ.ДатаВходящегоДокумента,
	|	ДокументПТиУ.НомерВходящегоДокумента,
	|	ВТ_ЗАчетАванса.СчетОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ЗачетАванса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблСчФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанные.Организация КАК Организация,
	|	ВТДанные.СчетОплаты КАК СчетОплаты,
	|	ВТДанные.Контрагент КАК Контрагент,
	|	ВТДанные.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТДанные.Документ КАК Документ,
	|	ВТДанные.РегНачОстатокДт КАК РегНачОстатокДт,
	|	ВТДанные.РегНачОстатокКт КАК РегНачОстатокКт,
	|	ВТДанные.РегПриход КАК РегПриход,
	|	ВТДанные.РегРасход КАК РегРасход,
	|	ВТДанные.РегКонОстатокДт КАК РегКонОстатокДт,
	|	ВТДанные.РегКонОстатокКт КАК РегКонОстатокКт,
	|	ВТДанные.РегПриходВал КАК РегПриходВал,
	|	ВТДанные.РегРасходВал КАК РегРасходВал,
	|	ВТДанные.РегКонОстатокДтВал КАК РегКонОстатокДтВал,
	|	ВТДанные.РегКонОстатокКтВал КАК РегКонОстатокКтВал,
	|	ВТДанные.РегНачОстатокДтВал КАК РегНачОстатокДтВал,
	|	ВТДанные.РегНачОстатокКтВал КАК РегНачОстатокКтВал,
	|	ВТДанные.ДатаСФ КАК ДатаСФ,
	|	ВТДанные.НомерСФ КАК НомерСФ,
	|	ВТДанные.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТДанные.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента
	|ИЗ
	|	ВТДанные КАК ВТДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетОплаты,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Документ
	|ИТОГИ
	|	СУММА(РегНачОстатокДт),
	|	СУММА(РегНачОстатокКт),
	|	СУММА(РегПриход),
	|	СУММА(РегРасход),
	|	СУММА(РегКонОстатокДт),
	|	СУММА(РегКонОстатокКт),
	|	СУММА(РегПриходВал),
	|	СУММА(РегРасходВал),
	|	СУММА(РегКонОстатокДтВал),
	|	СУММА(РегКонОстатокКтВал),
	|	СУММА(РегНачОстатокДтВал),
	|	СУММА(РегНачОстатокКтВал),
	|	МАКСИМУМ(ДатаСФ),
	|	МАКСИМУМ(НомерСФ),
	|	МАКСИМУМ(НомерВходящегоДокумента),
	|	МАКСИМУМ(ДатаВходящегоДокумента)
	|ПО
	|	ОБЩИЕ,
	|	СчетОплаты,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанные";
	
	Запрос.УстановитьПараметр("Начало", 		НачалоГода(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Конец", 			Новый Граница(КонецГода(ТекущаяДатаСеанса()), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", 	Документ.Организация);
	
	Счета_60_62 = Новый СписокЗначений;
	Счета_60_62.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками); 
	Счета_60_62.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	Счета_60_62.Добавить(ПланыСчетов.Хозрасчетный.АрендныеПлатежи); 
	
	Запрос.УстановитьПараметр("Счет76072", ПланыСчетов.Хозрасчетный.АрендныеПлатежи);
	
	Запрос.УстановитьПараметр("Счета_60_62", 			Счета_60_62);
	Запрос.УстановитьПараметр("Счет76", 				ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	Запрос.УстановитьПараметр("Руб", 					Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Контрагент", 			Документ.Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", 	Документ.ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Счет60", 				ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками);
	Запрос.УстановитьПараметр("Счет6021", 				ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиВал);
	Запрос.УстановитьПараметр("Счет6031", 				ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУЕ); 
	
	Счета6002_6201 = Новый СписокЗначений();
	Счета6002_6201.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданным);
	Счета6002_6201.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
	Запрос.УстановитьПараметр("Счета6002_6201", Счета6002_6201);
	
	Результат 		= Запрос.Выполнить();
	ВыборкаИтоги 	= Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтоги.Следующий();
	
	ШапкаДКЗ 		= Макет.ПолучитьОбласть("ШапкаДКЗ");
	ГруппировкаДКЗ 	= Макет.ПолучитьОбласть("ГруппировкаДКЗ");
	СтрокаДКЗ		= Макет.ПолучитьОбласть("СтрокаДКЗ");
	СтрокаДКЗВыд	= Макет.ПолучитьОбласть("СтрокаДКЗВыд");
	ИтогоДКЗ		= Макет.ПолучитьОбласть("ИтогоДКЗ");
	
	ТабДокумент.Вывести(ШапкаДКЗ);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	
	МассивРасчетныхДокументов = Документ.ДокументыРасчетов.Выгрузить().ВыгрузитьКолонку("ДокументРасчетовСКонтрагентом");
	
	ВыборкаСчет = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	Пока ВыборкаСчет.Следующий() Цикл
		
		ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаСчет);
		ГруппировкаДКЗ.Параметры.Группировка = ВыборкаСчет.СчетОплаты;
		ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаСчет.Уровень());
		
		ВыборкаКонтрагент = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
		
		Пока ВыборкаКонтрагент.Следующий() Цикл
			
			ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаКонтрагент);
			ГруппировкаДКЗ.Параметры.Группировка = ВыборкаКонтрагент.Контрагент;
			ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаКонтрагент.Уровень());
			
			ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
			
			Пока ВыборкаДоговор.Следующий() Цикл
				
				ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаДоговор);
				ГруппировкаДКЗ.Параметры.Группировка = ВыборкаДоговор.ДоговорКонтрагента;
				ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаДоговор.Уровень());
				
				ВыборкаДокумент = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаДокумент.Следующий() Цикл       
					
					Если МассивРасчетныхДокументов.Найти(ВыборкаДокумент.Документ) <> Неопределено Тогда
						
						СтрокаДКЗВыд.Параметры.Заполнить(ВыборкаДокумент);
						
						ТабДокумент.Вывести(СтрокаДКЗВыд); 
						
					Иначе         
						
						СтрокаДКЗ.Параметры.Заполнить(ВыборкаДокумент);	
						
						ТабДокумент.Вывести(СтрокаДКЗ); 
						
					КонецЕсли; 
					
				КонецЦикла;  
				
			КонецЦикла;  
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	ИтогоДКЗ.Параметры.Заполнить(ВыборкаИтоги);
	
	ТабДокумент.Вывести(ИтогоДКЗ);
	
КонецПроцедуры

Процедура ВывестиОплатуПоСчету(ТабДокумент, Макет, Документ)  
	
	ДоговорКонтрагента  = Документ.ДоговорКонтрагента;
	Контрагент			= Документ.Контрагент;
	Дата				= Документ.Дата;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента)
		И (СтрНайти(НРег(ДоговорКонтрагента.Наименование), "счет ") <> 0
		ИЛИ СтрНайти(НРег(ДоговорКонтрагента.Наименование), "счету ") <> 0) Тогда
		
		СуммаЗаявок6Месяцев = 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсЗаявкаНаОплату.Ссылка КАК Ссылка,
		|	игсЗаявкаНаОплату.ДоговорКонтрагента.Наименование КАК Наименование,
		|	игсЗаявкаНаОплату.СуммаДокумента КАК СуммаДокумента
		|ИЗ
		|	Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
		|ГДЕ
		|	игсЗаявкаНаОплату.Проведен
		|	И игсЗаявкаНаОплату.Контрагент = &Контрагент
		|	И игсЗаявкаНаОплату.Дата МЕЖДУ &Дата1 И &Дата2
		|	И игсЗаявкаНаОплату.Ссылка <> &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ"; 
		
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("Ссылка", 	Документ);
		Запрос.УстановитьПараметр("Дата1", 		Дата(2021, 8, 18));
		Запрос.УстановитьПараметр("Дата2", 		КонецДня(Дата));
		
		Результат 	= Запрос.Выполнить();
		Выборка 	= Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если (СтрНайти(НРег(Выборка.Наименование), "счет ") <> 0
				ИЛИ СтрНайти(НРег(Выборка.Наименование), "счету ") <> 0) Тогда
				
				СуммаЗаявок6Месяцев = СуммаЗаявок6Месяцев + Выборка.СуммаДокумента;	
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Документ.СуммаДокумента + СуммаЗаявок6Месяцев > 100000 Тогда
			
			Шапка 		= Макет.ПолучитьОбласть("ШапкаОплатаПоСчету");
			Строка		= Макет.ПолучитьОбласть("СтрокаОплатаПоСчету");
			Итоги		= Макет.ПолучитьОбласть("ИтогиОплатаПоСчету");
			
			ТабДокумент.Вывести(Шапка);
			
			Выборка.Сбросить();   
			
			Пока Выборка.Следующий() Цикл
				
				Если (СтрНайти(НРег(Выборка.Наименование), "счет ") <> 0
					ИЛИ СтрНайти(НРег(Выборка.Наименование), "счету ") <> 0) Тогда
					
					СуммаЗаявок6Месяцев = СуммаЗаявок6Месяцев + Выборка.СуммаДокумента;	
					
					Строка.Параметры.Заполнить(Выборка);
					
					ТабДокумент.Вывести(Строка);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Итоги.Параметры.СуммаДокумента = Документ.СуммаДокумента + СуммаЗаявок6Месяцев;   
			
			ТабДокумент.Вывести(Итоги);
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ПолучитьТаблицуОплат(Ссылка, ПараметрыДляТаблицыОплат)
	
	Возврат Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.игсПроект КАК игсПроект,
	|	ПоступлениеТоваровУслугУслуги.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПоступлениеТоваровУслугУслуги.Сумма КАК Сумма,
	|	ПоступлениеТоваровУслугУслуги.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслугУслуги.СчетЗатрат КАК СчетЗатрат,
	|	ПоступлениеТоваровУслугУслуги.игсОбъектРаспределения КАК игсОбъектРаспределения
	|ПОМЕСТИТЬ ВТ_ПТиУ
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|		ПО ПоступлениеТоваровУслуг.Ссылка = ПоступлениеТоваровУслугУслуги.Ссылка
	|ГДЕ
	|	НЕ ПоступлениеТоваровУслуг.ПометкаУдаления
	|	И ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	игсЗаявка.Ссылка КАК ЗаявкаСсылка,
	|	ВТ_ПТиУ.Ссылка КАК ПТиУСсылка,
	|	ВТ_ПТиУ.игсПроект КАК ПТиУНоменклатурнаяГруппа,
	|	игсЗаявка.ДокументОснование КАК ЗаявкаДокументОснование,
	|	игсЗаявка.Контрагент КАК ЗаявкаКонтрагент,
	|	игсЗаявка.СуммаДокумента КАК ЗаявкаСуммаДокумента,
	|	ВЫБОР
	|		КОГДА ВТ_ПТиУ.НомерСтроки ЕСТЬ NULL
	|			ТОГДА ""ТОВАРЫ""
	|		ИНАЧЕ ""УСЛУГИ""
	|	КОНЕЦ КАК Тип,
	|	ВЫБОР
	|		КОГДА ВТ_ПТиУ.НомерСтроки ЕСТЬ NULL
	|			ТОГДА ""НАКЛАДНЫЕ""
	|		КОГДА ВТ_ПТиУ.СчетЗатрат = &Счет20
	|			ТОГДА ""ПРЯМЫЕ""
	|		ИНАЧЕ ""НАКЛАДНЫЕ""
	|	КОНЕЦ КАК ВидЗатрат,
	|	ВТ_ПТиУ.игсОбъектРаспределения КАК игсОбъектРаспределения,
	|	ВЫБОР
	|		КОГДА ВТ_ПТиУ.СуммаВключаетНДС
	|			ТОГДА ВТ_ПТиУ.Сумма
	|		ИНАЧЕ ВТ_ПТиУ.Сумма + ВТ_ПТиУ.СуммаНДС
	|	КОНЕЦ КАК СуммаПоПТиУ
	|ПОМЕСТИТЬ ВТ_ЗаявкаПТиУ
	|ИЗ
	|	Документ.игсЗаявкаНаОплату КАК игсЗаявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.игсЗаявкаНаОплату.ДокументыРасчетов КАК игсЗаявкаДокументыРасчетов
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПТиУ КАК ВТ_ПТиУ
	|			ПО игсЗаявкаДокументыРасчетов.ДокументРасчетовСКонтрагентом = ВТ_ПТиУ.Ссылка
	|		ПО игсЗаявка.Ссылка = игсЗаявкаДокументыРасчетов.Ссылка
	|ГДЕ
	|	игсЗаявка.Ссылка = &Ссылка
	|	И НЕ игсЗаявка.ПометкаУдаления
	|	И игсЗаявка.Проведен
	|	И игсЗаявка.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка КАК Ссылка,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто КАК Субконто,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ВТ_ЗаявкаПТиУ.ПТиУСсылка КАК ПТиУСсылка
	|ПОМЕСТИТЬ ПТиУ_имп
	|ИЗ
	|	Документ.ИмпРеализацияСтроительныхРаботУслуг.Услуги КАК ИмпРеализацияСтроительныхРаботУслугУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаявкаПТиУ КАК ВТ_ЗаявкаПТиУ
	|		ПО (ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа = ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто)
	|			И (ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ИмпВидыОперацийРеализацияСтроительныхРаботУслуг.ПередачаЗаконченныхСМР))
	|ГДЕ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.Дата <= &ДатаЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозОборотыДтКт.Регистратор КАК Регистратор,
	|	ХозОборотыДтКт.СуммаОборот КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_ХозОборотыДтКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, &ДатаЗаявки, Регистратор, СчетДт = &СчетДт62, , СчетКт = &СчетКт51, , Организация = &Организация) КАК ХозОборотыДтКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ХозОборотыДтКт.Регистратор КАК Регистратор,
	|	ПТиУ_имп.Субконто КАК Субконто,
	|	ПТиУ_имп.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ПТиУ_имп.ПТиУСсылка КАК ПТиУСсылка,
	|	ПТиУ_имп.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Хоз_ПТиУ_имп
	|ИЗ
	|	ВТ_ХозОборотыДтКт КАК ВТ_ХозОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПТиУ_имп КАК ПТиУ_имп
	|		ПО (ПТиУ_имп.Ссылка = ВТ_ХозОборотыДтКт.Регистратор)
	|			И (ВТ_ХозОборотыДтКт.СуммаОборот > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка КАК Ссылка,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто КАК Субконто,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ВТ_ЗаявкаПТиУ.ПТиУСсылка КАК ПТиУСсылка
	|ПОМЕСТИТЬ ПТиУ_имп_ПоОР
	|ИЗ
	|	Документ.ИмпРеализацияСтроительныхРаботУслуг.Услуги КАК ИмпРеализацияСтроительныхРаботУслугУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаявкаПТиУ КАК ВТ_ЗаявкаПТиУ
	|		ПО (ВТ_ЗаявкаПТиУ.игсОбъектРаспределения = ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто)
	|			И (ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ИмпВидыОперацийРеализацияСтроительныхРаботУслуг.ПередачаЗаконченныхСМР))
	|ГДЕ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.Дата <= &ДатаЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ХозОборотыДтКт.Регистратор КАК Регистратор,
	|	ПТиУ_имп_ПоОР.Субконто КАК Субконто,
	|	ПТиУ_имп_ПоОР.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ПТиУ_имп_ПоОР.ПТиУСсылка КАК ПТиУСсылка,
	|	ПТиУ_имп_ПоОР.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Хоз_ПТиУ_имп_ПоОР
	|ИЗ
	|	ПТиУ_имп_ПоОР КАК ПТиУ_имп_ПоОР
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ХозОборотыДтКт КАК ВТ_ХозОборотыДтКт
	|		ПО ПТиУ_имп_ПоОР.Ссылка = ВТ_ХозОборотыДтКт.Регистратор
	|			И (ВТ_ХозОборотыДтКт.СуммаОборот > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	игсРезультатРаспределенияПоПроизводственнойПрограмме.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	игсРезультатРаспределенияПоПроизводственнойПрограмме.СправкаРаспределения КАК СправкаРаспределения,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ВТ_ЗаявкаПТиУ.ПТиУСсылка КАК ПТиУСсылка
	|ПОМЕСТИТЬ ПТиУ_Резуль
	|ИЗ
	|	ВТ_ЗаявкаПТиУ КАК ВТ_ЗаявкаПТиУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.игсРезультатРаспределенияПоПроизводственнойПрограмме КАК игсРезультатРаспределенияПоПроизводственнойПрограмме
	|		ПО (игсРезультатРаспределенияПоПроизводственнойПрограмме.СправкаРаспределения = ВТ_ЗаявкаПТиУ.ПТиУСсылка)
	|ГДЕ
	|	игсРезультатРаспределенияПоПроизводственнойПрограмме.Период <= &ДатаЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка КАК Ссылка,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто КАК Субконто,
	|	ПТиУ_Резуль.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ПТиУ_Резуль.ПТиУСсылка КАК ПТиУСсылка
	|ПОМЕСТИТЬ ПТиУ_Резуль_имп
	|ИЗ
	|	Документ.ИмпРеализацияСтроительныхРаботУслуг.Услуги КАК ИмпРеализацияСтроительныхРаботУслугУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПТиУ_Резуль КАК ПТиУ_Резуль
	|		ПО ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто = ПТиУ_Резуль.НоменклатурнаяГруппа
	|			И (ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ИмпВидыОперацийРеализацияСтроительныхРаботУслуг.ПередачаЗаконченныхСМР))
	|ГДЕ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка.Дата <= &ДатаЗаявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ХозОборотыДтКт.Регистратор КАК Регистратор,
	|	ПТиУ_Резуль_имп.Субконто КАК Субконто,
	|	ПТиУ_Резуль_имп.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ПТиУ_Резуль_имп.ПТиУСсылка КАК ПТиУСсылка,
	|	ПТиУ_Резуль_имп.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Хоз_ПТиУ_Резуль_имп
	|ИЗ
	|	ПТиУ_Резуль_имп КАК ПТиУ_Резуль_имп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ХозОборотыДтКт КАК ВТ_ХозОборотыДтКт
	|		ПО ПТиУ_Резуль_имп.Ссылка = ВТ_ХозОборотыДтКт.Регистратор
	|			И (ВТ_ХозОборотыДтКт.СуммаОборот > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка КАК ЗаявкаСсылка,
	|	ВТ_ЗаявкаПТиУ.ПТиУСсылка КАК ПТиУСсылка,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаКонтрагент КАК ЗаявкаКонтрагент,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСуммаДокумента КАК ЗаявкаСуммаДокумента,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА НЕ Хоз_ПТиУ_имп.Регистратор ЕСТЬ NULL
	|				ТОГДА Хоз_ПТиУ_имп.Регистратор
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НЕ Хоз_ПТиУ_имп_ПоОР.Регистратор ЕСТЬ NULL
	|						ТОГДА Хоз_ПТиУ_имп_ПоОР.Регистратор
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ Хоз_ПТиУ_Резуль_имп.Регистратор ЕСТЬ NULL
	|								ТОГДА Хоз_ПТиУ_Резуль_имп.Регистратор
	|							ИНАЧЕ NULL
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОНЕЦ) КАК КоличествоПлатежей,
	|	ВЫБОР
	|		КОГДА НЕ Хоз_ПТиУ_имп.Субконто ЕСТЬ NULL
	|			ТОГДА Хоз_ПТиУ_имп.Субконто
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ Хоз_ПТиУ_имп_ПоОР.Субконто ЕСТЬ NULL
	|					ТОГДА Хоз_ПТиУ_имп_ПоОР.Субконто
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ Хоз_ПТиУ_Резуль_имп.Субконто ЕСТЬ NULL
	|							ТОГДА Хоз_ПТиУ_Резуль_имп.Субконто
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ВТ_ЗаявкаПТиУ.Тип КАК Тип,
	|	ВТ_ЗаявкаПТиУ.ВидЗатрат КАК ВидЗатрат,
	|	ВТ_ЗаявкаПТиУ.СуммаПоПТиУ КАК СуммаПоПТиУ,
	|	ВЫБОР
	|		КОГДА НЕ Хоз_ПТиУ_имп.Ссылка ЕСТЬ NULL
	|			ТОГДА Хоз_ПТиУ_имп.Ссылка
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ Хоз_ПТиУ_имп_ПоОР.Ссылка ЕСТЬ NULL
	|					ТОГДА Хоз_ПТиУ_имп_ПоОР.Ссылка
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ Хоз_ПТиУ_Резуль_имп.Ссылка ЕСТЬ NULL
	|							ТОГДА Хоз_ПТиУ_Резуль_имп.Ссылка
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СправкаРаспределения
	|ПОМЕСТИТЬ ВТ_Промежуточные
	|ИЗ
	|	ВТ_ЗаявкаПТиУ КАК ВТ_ЗаявкаПТиУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Хоз_ПТиУ_имп КАК Хоз_ПТиУ_имп
	|		ПО ВТ_ЗаявкаПТиУ.ЗаявкаСсылка = Хоз_ПТиУ_имп.ЗаявкаСсылка
	|			И ВТ_ЗаявкаПТиУ.ПТиУСсылка = Хоз_ПТиУ_имп.ПТиУСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Хоз_ПТиУ_имп_ПоОР КАК Хоз_ПТиУ_имп_ПоОР
	|		ПО ВТ_ЗаявкаПТиУ.ЗаявкаСсылка = Хоз_ПТиУ_имп_ПоОР.ЗаявкаСсылка
	|			И ВТ_ЗаявкаПТиУ.ПТиУСсылка = Хоз_ПТиУ_имп_ПоОР.ПТиУСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Хоз_ПТиУ_Резуль_имп КАК Хоз_ПТиУ_Резуль_имп
	|		ПО ВТ_ЗаявкаПТиУ.ЗаявкаСсылка = Хоз_ПТиУ_Резуль_имп.ЗаявкаСсылка
	|			И ВТ_ЗаявкаПТиУ.ПТиУСсылка = Хоз_ПТиУ_Резуль_имп.ПТиУСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка,
	|	ВТ_ЗаявкаПТиУ.ПТиУСсылка,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаКонтрагент,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСуммаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ Хоз_ПТиУ_имп.Субконто ЕСТЬ NULL
	|			ТОГДА Хоз_ПТиУ_имп.Субконто
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ Хоз_ПТиУ_имп_ПоОР.Субконто ЕСТЬ NULL
	|					ТОГДА Хоз_ПТиУ_имп_ПоОР.Субконто
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ Хоз_ПТиУ_Резуль_имп.Субконто ЕСТЬ NULL
	|							ТОГДА Хоз_ПТиУ_Резуль_имп.Субконто
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВТ_ЗаявкаПТиУ.Тип,
	|	ВТ_ЗаявкаПТиУ.ВидЗатрат,
	|	ВТ_ЗаявкаПТиУ.СуммаПоПТиУ,
	|	ВЫБОР
	|		КОГДА НЕ Хоз_ПТиУ_имп.Ссылка ЕСТЬ NULL
	|			ТОГДА Хоз_ПТиУ_имп.Ссылка
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ Хоз_ПТиУ_имп_ПоОР.Ссылка ЕСТЬ NULL
	|					ТОГДА Хоз_ПТиУ_имп_ПоОР.Ссылка
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ Хоз_ПТиУ_Резуль_имп.Ссылка ЕСТЬ NULL
	|							ТОГДА Хоз_ПТиУ_Резуль_имп.Ссылка
	|						ИНАЧЕ НЕОПРЕДЕЛЕНО
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаявкаПТиУ.ЗаявкаКонтрагент КАК ЗРДСКонтрагент,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСсылка КАК ЗРДС,
	|	ЕСТЬNULL(ВТ_ЗаявкаПТиУ.ПТиУСсылка, ""Отсутствует"") КАК ПТИУ,
	|	ВТ_ЗаявкаПТиУ.Тип КАК Тип,
	|	ВТ_ЗаявкаПТиУ.ВидЗатрат КАК ВидЗатрат,
	|	ЕСТЬNULL(ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа, ""Не определена"") КАК НоменклатурнаяГруппа,
	|	ВТ_ЗаявкаПТиУ.ЗаявкаСуммаДокумента КАК ЗРДССуммаДокумента,
	|	ЕСТЬNULL(ВТ_Промежуточные.НоменклатурнаяГруппа.игсПроект, ""Не определён"") КАК Проект,
	|	ЕСТЬNULL(ВТ_ЗаявкаПТиУ.СуммаПоПТиУ, 0) КАК Сумма,
	|	ЕСТЬNULL(ВТ_Промежуточные.СправкаРаспределения, ""Не определена"") КАК СправкаРаспределения,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_Промежуточные.КоличествоПлатежей ЕСТЬ NULL
	|				И ВТ_Промежуточные.КоличествоПлатежей >= 1
	|			ТОГДА ""Оплата заказчиком произведена""
	|		КОГДА ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа = НЕОПРЕДЕЛЕНО
	|			ТОГДА ""Не распределено""
	|		КОГДА ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа ССЫЛКА Справочник.НоменклатурныеГруппы
	|				И ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|			ТОГДА ""Оплата заказчиком не произведена""
	|		ИНАЧЕ ""Статус оплаты не определен""
	|	КОНЕЦ КАК Категория,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_Промежуточные.КоличествоПлатежей ЕСТЬ NULL
	|				И ВТ_Промежуточные.КоличествоПлатежей >= 1
	|			ТОГДА 1
	|		КОГДА ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа = НЕОПРЕДЕЛЕНО
	|			ТОГДА 2
	|		КОГДА ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа ССЫЛКА Справочник.НоменклатурныеГруппы
	|				И ВТ_ЗаявкаПТиУ.ПТиУНоменклатурнаяГруппа <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК КатегорияЧисло,
	|	ЕСТЬNULL(ВТ_Промежуточные.КоличествоПлатежей, 0) КАК КоличествоПлатежей
	|ИЗ
	|	ВТ_ЗаявкаПТиУ КАК ВТ_ЗаявкаПТиУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Промежуточные КАК ВТ_Промежуточные
	|		ПО ВТ_ЗаявкаПТиУ.ЗаявкаСсылка = ВТ_Промежуточные.ЗаявкаСсылка
	|			И ВТ_ЗаявкаПТиУ.ПТиУСсылка = ВТ_Промежуточные.ПТиУСсылка";
	
	Запрос.УстановитьПараметр("Ссылка", 		Ссылка); 
	Запрос.УстановитьПараметр("ДатаЗаявки", 	ПараметрыДляТаблицыОплат.ДатаЗаявки);
	Запрос.УстановитьПараметр("СчетДт62", 		ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	Запрос.УстановитьПараметр("СчетКт51",		ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	Запрос.УстановитьПараметр("Счет20", 		ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	Запрос.УстановитьПараметр("Организация", 	ПараметрыДляТаблицыОплат.Организация);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Таблица = Результат.Выгрузить();
	КонецЕсли;
	
	Возврат Таблица;	
	
КонецФункции 

Процедура ВывестиОС(ТабДокумент, Макет, Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИГСАрендованныеОСОстаткиИОбороты.Контрагент КАК Контрагент,
	|	ИГСАрендованныеОСОстаткиИОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ИГСАрендованныеОСОстаткиИОбороты.Склад КАК Склад,
	|	ИГСАрендованныеОСОстаткиИОбороты.ОсновноеСредство,
	|	ИГСАрендованныеОСОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ИГСАрендованныеОСОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	|	ИГСАрендованныеОСОстаткиИОбороты.СуммаПриход КАК СуммаПриход,
	|	ИГСАрендованныеОСОстаткиИОбороты.СуммаРасход КАК СуммаРасход
	|ИЗ
	|	РегистрНакопления.ИГСАрендованныеОС.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , ДоговорКонтрагента = &ДоговорКонтрагента) КАК ИГСАрендованныеОСОстаткиИОбороты
	|ИТОГИ
	|	СУММА(СуммаНачальныйОстаток),
	|	СУММА(СуммаКонечныйОстаток),
	|	СУММА(СуммаПриход),
	|	СУММА(СуммаРасход)
	|ПО
	|	ОБЩИЕ,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Склад";
	
	Запрос.УстановитьПараметр("НачалоПериода", 			НачалоГода(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("КонецПериода", 			КонецГода(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ДоговорКонтрагента", 	Документ.ДоговорКонтрагента);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ВыборкаИтоги 	= Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтоги.Следующий();
	
	ШапкаДКЗ 		= Макет.ПолучитьОбласть("ШапкаОС");
	ГруппировкаДКЗ 	= Макет.ПолучитьОбласть("ГруппировкаОС");
	СтрокаДКЗ		= Макет.ПолучитьОбласть("СтрокаОС");
	ИтогоДКЗ		= Макет.ПолучитьОбласть("ИтогоОС");
	
	ТабДокумент.Вывести(ШапкаДКЗ);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	
	ВыборкаКонтрагент = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	Пока ВыборкаКонтрагент.Следующий() Цикл
		
		ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаКонтрагент);
		ГруппировкаДКЗ.Параметры.Группировка = ВыборкаКонтрагент.Контрагент;
		ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаКонтрагент.Уровень());
		
		ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
		
		Пока ВыборкаДоговор.Следующий() Цикл
			
			ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаДоговор);
			ГруппировкаДКЗ.Параметры.Группировка = ВыборкаДоговор.ДоговорКонтрагента;
			ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаДоговор.Уровень());
			
			ВыборкаСклад = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
			
			Пока ВыборкаСклад.Следующий() Цикл
				
				ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаСклад);
				ГруппировкаДКЗ.Параметры.Группировка = ВыборкаСклад.Склад;
				ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаСклад.Уровень());
				
				Выборка = ВыборкаСклад.Выбрать(); 
				
				Пока Выборка.Следующий() Цикл
					
					СтрокаДКЗ.Параметры.Заполнить(Выборка);
					СтрокаДКЗ.Параметры.Группировка = Выборка.ОсновноеСредство;
					ТабДокумент.Вывести(СтрокаДКЗ, Выборка.Уровень());
					
				КонецЦикла; 
				
			КонецЦикла;
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	ИтогоДКЗ.Параметры.Заполнить(ВыборкаИтоги);	
	
	ТабДокумент.Вывести(ИтогоДКЗ);
	
КонецПроцедуры

Процедура ВывестиКопииОригиналы(ТабДокумент, Макет, Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Номер КАК Номер,
	|	ПоступлениеТоваровУслуг.ВидОперации КАК ВидОперации,
	|	ПоступлениеТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Контрагент,
	|	ПоступлениеТоваровУслуг.Склад КАК Склад,
	|	ПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	""УСЛУГИ"" КАК ТипПоступления
	|ПОМЕСТИТЬ ВТДанные
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияБух КАК ОперацияБух
	|			ПО ПоступлениеТоваровУслуг.Ссылка = ОперацияБух.СторнируемыйДокумент
	|		ПО ПоступлениеТоваровУслугУслуги.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ ОперацияБух.ПометкаУдаления
	|	И ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслуг.Номер,
	|	ПоступлениеТоваровУслуг.ВидОперации,
	|	ПоступлениеТоваровУслуг.СуммаДокумента,
	|	ПоступлениеТоваровУслуг.Контрагент,
	|	ПоступлениеТоваровУслуг.Склад,
	|	ПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента,
	|	""ТМЦ""
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ПоступлениеТоваровУслуг.Склад = Склады.Ссылка
	|				И ПоступлениеТоваровУслуг.Склад = Склады.Ссылка
	|		ПО ПоступлениеТоваровУслугУслуги.Ссылка = ПоступлениеТоваровУслуг.Ссылка,
	|	Документ.ОперацияБух КАК ОперацияБух
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Проведен
	|	И НЕ ОперацияБух.ПометкаУдаления
	|	И ОперацияБух.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОСновныеСредства)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанные.Ссылка КАК Ссылка,
	|	ВТДанные.Дата КАК Дата,
	|	ВТДанные.Номер КАК Номер,
	|	ВТДанные.ВидОперации КАК ВидОперации,
	|	ВТДанные.СуммаДокумента КАК СуммаДокумента,
	|	ВТДанные.Контрагент КАК Контрагент,
	|	ВТДанные.Склад КАК Склад,
	|	ВТДанные.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВТДанные.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТДанные.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТДанные.ТипПоступления КАК ТипПоступления
	|ИЗ
	|	ВТДанные КАК ВТДанные
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ТипПоступления = ""Услуги""
	|				ТОГДА ВТДанные.ТипПоступления = ""Услуги""
	|			КОГДА &ТипПоступления = ""ТМЦ""
	|				ТОГДА ВТДанные.ТипПоступления = ""ТМЦ""
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВТДанные.Контрагент = &Контрагент
	|	И ВТДанные.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку)
	|ИТОГИ
	|	СУММА(СуммаДокумента)
	|ПО
	|	ТипПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанные";  
	
	Запрос.УстановитьПараметр("НачалоПериода", 		Дата(2017, 1, 1));
	Запрос.УстановитьПараметр("КонецПериода", 		Дата(2999, 1, 1));
	Запрос.УстановитьПараметр("ТипПоступления", 	"Все");
	Запрос.УстановитьПараметр("Контрагент", 		Документ.Контрагент);
	
	Результат = Запрос.Выполнить(); 
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ВыборкаИтоги 	= Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтоги.Следующий();
	
	ШапкаДКЗ 		= Макет.ПолучитьОбласть("ШапкаКопияОригинал");
	ГруппировкаДКЗ 	= Макет.ПолучитьОбласть("ГруппировкаКопияОригинал");
	СтрокаДКЗ		= Макет.ПолучитьОбласть("СтрокаКопияОригинал");
	ИтогоДКЗ		= Макет.ПолучитьОбласть("ИтогоКопияОригинал");
	
	ТабДокумент.Вывести(ШапкаДКЗ);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	
	ВыборкаТип = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	Пока ВыборкаТип.Следующий() Цикл
		
		ГруппировкаДКЗ.Параметры.Заполнить(ВыборкаТип);
		ГруппировкаДКЗ.Параметры.Группировка = ВыборкаТип.ТипПоступления; 
		
		ТабДокумент.Вывести(ГруппировкаДКЗ, ВыборкаТип.Уровень());
		
		Выборка = ВыборкаТип.Выбрать();  
		
		Пока Выборка.Следующий() Цикл    
			
			СтрокаДКЗ.Параметры.Заполнить(Выборка);	
			
			ТабДокумент.Вывести(СтрокаДКЗ);  
			
		КонецЦикла;   
		
	КонецЦикла; 
	
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	ИтогоДКЗ.Параметры.Заполнить(ВыборкаИтоги);	
	
	ТабДокумент.Вывести(ИтогоДКЗ);
	
КонецПроцедуры

Процедура ВывестиРанееПроведенныеОплаты(ТабДокумент, Макет, Документ)
	
	Если ЗначениеЗаполнено(Документ.ДоговорКонтрагента.Сумма) Тогда
		
		ОбластьШапка 	= Макет.ПолучитьОбласть("ШапкаОплаты");
		ОбластьСтрока 	= Макет.ПолучитьОбласть("СтрокаОплаты");
		ОбластьИтоги 	= Макет.ПолучитьОбласть("ИтогиОплаты"); 
		
		ОбластьШапка.Параметры.СуммаДоговора = Формат(Документ.ДоговорКонтрагента.Сумма, "ЧЦ=15; ЧДЦ=2");
		
		СубконтоКонтрагентДоговор = Новый СписокЗначений;
		СубконтоКонтрагентДоговор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		СубконтоКонтрагентДоговор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры); 
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Контрагент",					Документ.Контрагент);
		Запрос.УстановитьПараметр("ДоговорКонтрагента", 		Документ.ДоговорКонтрагента);
		Запрос.УстановитьПараметр("СубконтоКонтрагентДоговор", 	СубконтоКонтрагентДоговор);
		Запрос.УстановитьПараметр("КонечнаяДата", 				Документ.Дата); 
		Запрос.УстановитьПараметр("Счет60", 					ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
		Запрос.УстановитьПараметр("Счет76", 					ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
		Запрос.УстановитьПараметр("Счет51", 					ПланыСчетов.Хозрасчетный.РасчетныеСчета);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	игсКазнаРегистрСостоянийСрезПоследних.Заявка КАК Заявка
		|ПОМЕСТИТЬ игсКазнаРегистрСостоянийСрезПоследних
		|ИЗ
		|	РегистрСведений.игсКазнаРегистрСостояний.СрезПоследних(&КонечнаяДата, ЕСТЬNULL(СостояниеЗаявки, ЗНАЧЕНИЕ(Перечисление.игсСостоянияСогласования.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.игсСостоянияСогласования.Отклонена)) КАК игсКазнаРегистрСостоянийСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Период КАК Период,
		|	ХозрасчетныйОбороты.Регистратор КАК Регистратор,
		|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаОборот
		|ПОМЕСТИТЬ ХозрасчетныйОбороты
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(, , Регистратор, Счет В ИЕРАРХИИ (&Счет60, &Счет76), &СубконтоКонтрагентДоговор, Субконто2 = &ДоговорКонтрагента, КорСчет = &Счет51, ) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГОД(игсЗаявкаНаОплату.Дата) КАК ГОД,
		|	игсЗаявкаНаОплату.СуммаДокумента КАК Сумма,
		|	игсЗаявкаНаОплату.Ссылка КАК Ссылка,
		|	ПОДСТРОКА(игсЗаявкаНаОплату.НазначениеПлатежа, 1, 210) КАК НазначениеПлатежа
		|ИЗ
		|	Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|		ПО (ПлатежноеПоручение.ДокументОснование = игсЗаявкаНаОплату.Ссылка)
		|			И (ПлатежноеПоручение.Проведен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ игсКазнаРегистрСостоянийСрезПоследних КАК игсКазнаРегистрСостоянийСрезПоследних
		|		ПО игсЗаявкаНаОплату.Ссылка = игсКазнаРегистрСостоянийСрезПоследних.Заявка
		|ГДЕ
		|	игсЗаявкаНаОплату.Ссылка.Проведен
		|	И игсЗаявкаНаОплату.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ПлатежноеПоручение.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГОД(ХозрасчетныйОбороты.Период),
		|	ХозрасчетныйОбороты.СуммаОборот,
		|	ХозрасчетныйОбороты.Регистратор,
		|	ЕСТЬNULL(ПОДСТРОКА(ПлатежноеПоручение.НазначениеПлатежа, 1, 210), """")
		|ИЗ
		|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|		ПО (ПлатежноеПоручение.Ссылка = ХозрасчетныйОбороты.Регистратор)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	ОБЩИЕ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		ВыведенаШапка 	= Ложь;
		
		Результат 		= Запрос.Выполнить();
		ВыборкаИтоги 	= Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		
		Пока ВыборкаИтоги.Следующий() Цикл
			
			ОбластьИтоги.Параметры.Сумма 			= ВыборкаИтоги.Сумма;
			ОбластьШапка.Параметры.СуммаОстаток 	= Формат(Документ.ДоговорКонтрагента.Сумма - ВыборкаИтоги.Сумма, "ЧЦ=15; ЧДЦ=2"); 
			
			ТабДокумент.Вывести(ОбластьШапка);   
			
			ВыведенаШапка = Истина;
			
			Выборка = ВыборкаИтоги.Выбрать(); 
			
			Пока Выборка.Следующий() Цикл   
				
				ОбластьСтрока.Параметры.Заполнить(Выборка); 
				
				ТабДокумент.Вывести(ОбластьСтрока); 
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если Не ВыведенаШапка Тогда
			ТабДокумент.Вывести(ОбластьШапка);
		Иначе
			ТабДокумент.Вывести(ОбластьИтоги);
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ПолучитьПроектВерхнегоУровня(Проект)
	
	//Если ТипЗнч(НаименованиеПроекта) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
	//	
	//	Если НЕ ЗначениеЗаполнено(НаименованиеПроекта) 
	//		ИЛИ НаименованиеПроекта.Уровень() = 0 Тогда
	//		
	//		Возврат НаименованиеПроекта; 
	//		
	//	Иначе
	//		Если Строка(НаименованиеПроекта.Родитель) = "ПРОЕКТЫ" Тогда
	//			
	//			Возврат НаименованиеПроекта;  
	//			
	//		Иначе   
	//			
	//			Возврат ПолучитьПроектВерхнегоУровня(НаименованиеПроекта.Родитель); 
	//			
	//		КонецЕсли; 
	//		
	//	КонецЕсли; 
	//	
	//КонецЕсли; 
	Сообщить("до  " + Проект);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
		|	НоменклатурныеГруппы.Родитель.Представление КАК РодительПредставление
		|ИЗ
		|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
		|ГДЕ
		|	НоменклатурныеГруппы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Проект); 
	
	Результат 	= Запрос.Выполнить();
	Выборка 	= Результат.Выбрать();  
	
	Пока Выборка.Следующий() Цикл
		Проект = Выборка.РодительПредставление;
	КонецЦикла;
	
	Сообщить("после  " + Проект);
	
	Возврат Проект;
	
КонецФункции

Функция ПолучитьКопиюОригиналДокумента(Документ) 
	
	КопияОригинал = ""; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	// <ИГС> ПР Потылицын Г.С. № 00-4210 14.01.2025 {
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеДопРасходов.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(игсУчетНаличияЭкземпляровПервичныхДокументов.ВидЭкземпляра, ""Не указано"") КАК ВидЭкземпляра
	|ПОМЕСТИТЬ ПоступлениеДопРасходов
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.игсУчетНаличияЭкземпляровПервичныхДокументов КАК игсУчетНаличияЭкземпляровПервичныхДокументов
	|		ПО ПоступлениеДопРасходов.Ссылка = игсУчетНаличияЭкземпляровПервичныхДокументов.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(игсУчетНаличияЭкземпляровПервичныхДокументов.ВидЭкземпляра, ""Не указано"") КАК ВидЭкземпляра
	|ПОМЕСТИТЬ ПоступлениеТоваровУслуг
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.игсУчетНаличияЭкземпляровПервичныхДокументов КАК игсУчетНаличияЭкземпляровПервичныхДокументов
	|		ПО ПоступлениеТоваровУслуг.Ссылка = игсУчетНаличияЭкземпляровПервичныхДокументов.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ВидЭкземпляра, ПоступлениеДопРасходов.ВидЭкземпляра) КАК КопияОригинал,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровУслуг.ВидЭкземпляра, ПоступлениеДопРасходов.ВидЭкземпляра) = ЗНАЧЕНИЕ(Перечисление.игсВидыЭкземпляраПервичногоДокумента.Копия)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Документ.игсЗаявкаНаОплату.ДокументыРасчетов КАК игсЗаявкаНаОплатуДокументыРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|		ПО (ПоступлениеДопРасходов.Ссылка = игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом)
	|ГДЕ
	|	игсЗаявкаНаОплатуДокументыРасчетов.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|АВТОУПОРЯДОЧИВАНИЕ";
	// } </ИГС>
	
	Запрос.УстановитьПараметр("Ссылка", Документ); 
	
	Результат = Запрос.Выполнить(); 
	
	Если Не Результат.Пустой() Тогда   
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();    
		
		Если ЗначениеЗаполнено(Выборка.КопияОригинал) Тогда
			КопияОригинал = Выборка.КопияОригинал;
		КонецЕсли;  
		
	КонецЕсли; 
	
	Возврат КопияОригинал;
	
КонецФункции

Функция ПолучитьПодОтчетЗаявки(СтатьяДДС)
	
	ПодОтчет = Ложь; 
	
	СсылкаИгсНастройка = ПланыВидовХарактеристик.игсНастройкиМеханизмов.СтатьиДДСБезРасчетного;
	
	ЗапросДДС = Новый Запрос;
	ЗапросДДС.Текст = 
	"ВЫБРАТЬ
	|	игсСпискиДляНастроекМеханизмовСписок.Значение КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.игсНастройкиМеханизмов КАК игсНастройкиМеханизмов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.игсСпискиДляНастроекМеханизмов КАК игсСпискиДляНастроекМеханизмов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.игсСпискиДляНастроекМеханизмов.Список КАК игсСпискиДляНастроекМеханизмовСписок
	|			ПО игсСпискиДляНастроекМеханизмов.Ссылка = игсСпискиДляНастроекМеханизмовСписок.Ссылка
	|		ПО игсНастройкиМеханизмов.Ссылка = игсСпискиДляНастроекМеханизмов.Владелец
	|ГДЕ
	|	игсНастройкиМеханизмов.Ссылка = &СсылкаИгсНастройка
	|	И игсСпискиДляНастроекМеханизмовСписок.Значение = &ЗначениеСтатьиДДС";
	
	ЗапросДДС.УстановитьПараметр("СсылкаИгсНастройка", СсылкаИгсНастройка);
	ЗапросДДС.УстановитьПараметр("ЗначениеСтатьиДДС", СтатьяДДС);
	
	РезультатДДС = ЗапросДДС.Выполнить();
	
	Если НЕ РезультатДДС.Пустой() Тогда 
		
		ПодОтчет = Истина;
		
	КонецЕсли;
	
	Возврат ПодОтчет;
	
КонецФункции

Функция ПолучитьПечатнуюФорму(Знач МассивОбъектов) Экспорт
	
	Возврат СоздатьТабличныйДокументЗРДС(МассивОбъектов);
	
КонецФункции