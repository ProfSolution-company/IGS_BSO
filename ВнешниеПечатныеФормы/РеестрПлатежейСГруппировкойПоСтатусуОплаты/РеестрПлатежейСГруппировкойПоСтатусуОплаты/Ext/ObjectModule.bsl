
Функция СведенияОВнешнейОбработке() Экспорт     
	
	ПараметрыРегистрации 					= ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.3.3.0");
	ПараметрыРегистрации.Информация 		= НСтр("ru = 'Реестр платежей с группировкой по статусу оплаты.'");
	ПараметрыРегистрации.Вид 				= ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия 			= "1.2";
	ПараметрыРегистрации.БезопасныйРежим 	= Ложь;
	ПараметрыРегистрации.Назначение.Добавить("Документ.игсРеестрыНаОплату");
	
	Команда 								= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление 					= НСтр("ru = 'Реестр платежей с группировкой по статусу оплаты'");
	Команда.Идентификатор 					= "РеестрПлатежейСГруппировкойПоСтатусуОплаты";
	Команда.Использование 					= ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение 			= Истина;
	Команда.Модификатор 					= "ПечатьMXL";
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "РеестрПлатежейСГруппировкойПоСтатусуОплаты");
	
	МассивОбъектовНаПечать = ПроверитьЗаполнениеТЧДокументов(МассивОбъектов);
	
	Если МассивОбъектовНаПечать.Количество() <> 0 Тогда 
		
		Если ПечатнаяФорма <> Неопределено Тогда
			
			ПечатнаяФорма.ТабличныйДокумент 	= ПечатьМакетаРеестраНаОплату(МассивОбъектовНаПечать, ОбъектыПечати, ПараметрыВывода);
			ПечатнаяФорма.СинонимМакета 		= НСтр("ru = 'Макет отчета'");  
			ПечатнаяФорма.ПолныйПутьКМакету 	= "Объект.Макеты.МакетОтчета";
			
		КонецЕсли; 
		
	Иначе
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент; 
		Возврат;
	КонецЕсли;
	
КонецПроцедуры      

Функция ПечатьМакетаРеестраНаОплату(МассивОбъектов,	ОбъектыПечати, ПараметрыВывода) 
	
	ЗапросРеестр = Новый Запрос;
	ЗапросРеестр.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсРеестрыНаОплату.Ссылка КАК Ссылка,
		|	игсРеестрыНаОплату.Организация КАК Организация,
		|	игсРеестрыНаОплату.Дата КАК Дата,
		|	игсРеестрыНаОплату.НомерРеестра КАК НомерРеестра,
		|	игсРеестрыНаОплату.Номер КАК Номер
		|ИЗ
		|	Документ.игсРеестрыНаОплату КАК игсРеестрыНаОплату
		|ГДЕ
		|	игсРеестрыНаОплату.Ссылка В(&Ссылка)";
	
	ЗапросРеестр.УстановитьПараметр("Ссылка", МассивОбъектов);
	
	РезультатРеестр = ЗапросРеестр.Выполнить();
	
	ВыборкаРеестр 	= РезультатРеестр.Выбрать();
	
	ТаблицаНастроек = ПолучитьТаблицуНастроек();
	
	ДокументРезультат 						= Новый ТабличныйДокумент; 
	ДокументРезультат.АвтоМасштаб 			= Истина;
	ДокументРезультат.КлючПараметровПечати 	= "ПАРАМЕТРЫ_ПЕЧАТИ_РеестрПлатежейСГруппировкойПоСтатусуОплаты";
	
	Пока ВыборкаРеестр.Следующий() Цикл
		
		МакетДляПечати = ПолучитьМакет("МакетОтчета");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсРеестрыНаОплатуСводныеОстатки.РасчетныйСчет КАК РасчетныйСчет,
		|	игсРеестрыНаОплатуСводныеОстатки.КоличествоДокументов КАК КоличествоДокументов,
		|	игсРеестрыНаОплатуСводныеОстатки.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
		|	игсРеестрыНаОплатуСводныеОстатки.СуммаПриход КАК СуммаПриход,
		|	игсРеестрыНаОплатуСводныеОстатки.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
		|	игсРеестрыНаОплатуСводныеОстатки.СуммаРасход КАК СуммаРасход
		|ПОМЕСТИТЬ ВТ_БанковскиеСчетаОрганизации
		|ИЗ
		|	Документ.игсРеестрыНаОплату.СводныеОстатки КАК игсРеестрыНаОплатуСводныеОстатки
		|ГДЕ
		|	игсРеестрыНаОплатуСводныеОстатки.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_БанковскиеСчетаОрганизации.РасчетныйСчет КАК РасчетныйСчет,
		|	ВТ_БанковскиеСчетаОрганизации.КоличествоДокументов КАК КоличествоДокументов,
		|	ВТ_БанковскиеСчетаОрганизации.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
		|	ВТ_БанковскиеСчетаОрганизации.СуммаПриход КАК СуммаПриход,
		|	ВТ_БанковскиеСчетаОрганизации.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
		|	ВТ_БанковскиеСчетаОрганизации.СуммаРасход КАК СуммаРасход,
		|	БанковскиеСчета.Валютный КАК Валютный,
		|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
		|	БанковскиеСчета.ВидСчета КАК ТипСчета
		|ИЗ
		|	ВТ_БанковскиеСчетаОрганизации КАК ВТ_БанковскиеСчетаОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ПО ВТ_БанковскиеСчетаОрганизации.РасчетныйСчет = БанковскиеСчета.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсКазнаРегистрСостоянийСрезПоследних.Заявка КАК Заявка,
		|	игсКазнаРегистрСостоянийСрезПоследних.СостояниеЗаявки КАК Статус
		|ПОМЕСТИТЬ ВТ_игсКазнаРегистрСостоянийСрезПоследних
		|ИЗ
		|	РегистрСведений.игсКазнаРегистрСостояний.СрезПоследних(&ДатаОтчета, ) КАК игсКазнаРегистрСостоянийСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента КАК Заявка,
		|	ПоступлениеТоваровУслугУслуги.игсОбъектРаспределения КАК игсОбъектРаспределения,
		|	ВЫБОР
		|		КОГДА игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента.ДокументОснование.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги)
		|			ТОГДА ВЫБОР
		|					КОГДА игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента.ДокументОснование.СчетУчетаРасчетовСКонтрагентом <> &Счет2001
		|						ТОГДА ""Неопределено""
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ ""Неопределено""
		|	КОНЕЦ КАК СтатусОплаты,
		|	игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
		|	игсРеестрыНаОплатуЗаявкиНаОплату.СуммаКОплате КАК СуммаКОплате,
		|	игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента.ДокументОснование КАК СсылкаДокументаДокументОснование,
		|	игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента.ДокументОснование.ВидОперации КАК СсылкаДокументаДокументОснованиеВидОперации,
		|	ВТ_игсКазнаРегистрСостоянийСрезПоследних.Статус КАК Статус,
		|	игсРеестрыНаОплатуЗаявкиНаОплату.Контрагент КАК Контрагент,
		|	игсЗаявкаНаОплату.НазначениеПлатежа КАК НазначениеПлатежа,
		|	игсЗаявкаНаОплату.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ПОМЕСТИТЬ ВТ_ЗаявкиИСчетаНаОплату
		|ИЗ
		|	Документ.игсРеестрыНаОплату.ЗаявкиНаОплату КАК игсРеестрыНаОплатуЗаявкиНаОплату
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.игсЗаявкаНаОплату.ДокументыРасчетов КАК игсЗаявкаНаОплатуДокументыРасчетов
		|		ПО игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента = игсЗаявкаНаОплатуДокументыРасчетов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ПО игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента.ДокументОснование = ПоступлениеТоваровУслугУслуги.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_игсКазнаРегистрСостоянийСрезПоследних КАК ВТ_игсКазнаРегистрСостоянийСрезПоследних
		|		ПО (ВТ_игсКазнаРегистрСостоянийСрезПоследних.Заявка = игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
		|		ПО игсРеестрыНаОплатуЗаявкиНаОплату.СсылкаДокумента = игсЗаявкаНаОплату.Ссылка
		|ГДЕ
		|	игсРеестрыНаОплатуЗаявкиНаОплату.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Вт_ЗаявкиИСчетаНаОплату.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
		|	игсНачальныеОстаткиОплаченныхЭтаповРабот.Организация КАК Организация,
		|	игсНачальныеОстаткиОплаченныхЭтаповРабот.Этап КАК Этап,
		|	Вт_ЗаявкиИСчетаНаОплату.СуммаКОплате КАК СуммаКОплате,
		|	""Оплачено"" КАК СтатусОплаты,
		|	Вт_ЗаявкиИСчетаНаОплату.Контрагент КАК Контрагент,
		|	Вт_ЗаявкиИСчетаНаОплату.НазначениеПлатежа КАК НазначениеПлатежа,
		|	Вт_ЗаявкиИСчетаНаОплату.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ПОМЕСТИТЬ ВТ_ОплаченныеЭтапыРабот
		|ИЗ
		|	ВТ_ЗаявкиИСчетаНаОплату КАК Вт_ЗаявкиИСчетаНаОплату
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.игсНачальныеОстаткиОплаченныхЭтаповРабот КАК игсНачальныеОстаткиОплаченныхЭтаповРабот
		|		ПО Вт_ЗаявкиИСчетаНаОплату.игсОбъектРаспределения = игсНачальныеОстаткиОплаченныхЭтаповРабот.Этап
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Вт_ЗаявкиИСчетаНаОплату.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
		|	Вт_ЗаявкиИСчетаНаОплату.СуммаКОплате КАК СуммаКОплате,
		|	"""" КАК СтатусОплаты,
		|	Вт_ЗаявкиИСчетаНаОплату.игсОбъектРаспределения КАК НоменклатурнаяГруппа,
		|	Вт_ЗаявкиИСчетаНаОплату.Контрагент КАК Контрагент,
		|	Вт_ЗаявкиИСчетаНаОплату.НазначениеПлатежа КАК НазначениеПлатежа,
		|	Вт_ЗаявкиИСчетаНаОплату.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ПОМЕСТИТЬ ВТ_НоменклатурныеГруппыДляПроверки
		|ИЗ
		|	ВТ_ЗаявкиИСчетаНаОплату КАК Вт_ЗаявкиИСчетаНаОплату
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.игсНачальныеОстаткиОплаченныхЭтаповРабот КАК игсНачальныеОстаткиОплаченныхЭтаповРабот
		|		ПО Вт_ЗаявкиИСчетаНаОплату.игсОбъектРаспределения = игсНачальныеОстаткиОплаченныхЭтаповРабот.Этап
		|ГДЕ
		|	игсНачальныеОстаткиОплаченныхЭтаповРабот.Этап ЕСТЬ NULL
		|	И Вт_ЗаявкиИСчетаНаОплату.СтатусОплаты <> ""Неопределено""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК СубконтоДт1,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1
		|ПОМЕСТИТЬ ВТ_ХозрасчетныйОборотыДтКт_ДтВ62
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, &ДатаОтчета, Запись, СчетДт В ИЕРАРХИИ (&Счет62), &СубконтоРасчетныеДок, СчетКт В ИЕРАРХИИ (&Счет46), &СубконтоНоменклатурныеГруппы, ) КАК ХозрасчетныйОборотыДтКт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НоменклатурныеГруппыДляПроверки.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
		|	ВТ_ХозрасчетныйОборотыДтКт_ДтВ62.СубконтоДт1 КАК ДокументРасчетов,
		|	ВТ_ХозрасчетныйОборотыДтКт_ДтВ62.СубконтоКт1 КАК НоменклатурнаяГруппа,
		|	ВТ_НоменклатурныеГруппыДляПроверки.Контрагент КАК Контрагент,
		|	ВТ_НоменклатурныеГруппыДляПроверки.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ВТ_НоменклатурныеГруппыДляПроверки.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ПОМЕСТИТЬ ВТ_НГДР
		|ИЗ
		|	ВТ_НоменклатурныеГруппыДляПроверки КАК ВТ_НоменклатурныеГруппыДляПроверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ХозрасчетныйОборотыДтКт_ДтВ62 КАК ВТ_ХозрасчетныйОборотыДтКт_ДтВ62
		|		ПО (ВТ_ХозрасчетныйОборотыДтКт_ДтВ62.СубконтоКт1 = ВТ_НоменклатурныеГруппыДляПроверки.НоменклатурнаяГруппа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаОборот,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1
		|ПОМЕСТИТЬ ВТ_ХозрасчетныйОборотыДтКт_ДтВ51
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, &ДатаОтчета, Запись, СчетДт В ИЕРАРХИИ (&Счета51_52), &Субконто51_52, СчетКт В ИЕРАРХИИ (&Счет62), &СубконтоРасчетныеДок, ) КАК ХозрасчетныйОборотыДтКт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НГДР.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
		|	ВЫБОР
		|		КОГДА ВТ_ХозрасчетныйОборотыДтКт_ДтВ51.СуммаОборот > 0
		|			ТОГДА ""Оплачено""
		|		ИНАЧЕ ""Неопределено""
		|	КОНЕЦ КАК СтатусОплаты,
		|	ВТ_ХозрасчетныйОборотыДтКт_ДтВ51.СуммаОборот КАК Сумма,
		|	ВТ_НГДР.Контрагент КАК Контрагент,
		|	ВТ_НГДР.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ВТ_НГДР.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ПОМЕСТИТЬ ВТ_ЗаявкиПоКоторымПроизведенаОплата
		|ИЗ
		|	ВТ_НГДР КАК ВТ_НГДР
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ХозрасчетныйОборотыДтКт_ДтВ51 КАК ВТ_ХозрасчетныйОборотыДтКт_ДтВ51
		|		ПО ВТ_НГДР.ДокументРасчетов = ВТ_ХозрасчетныйОборотыДтКт_ДтВ51.СубконтоКт1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.ДокументРасчетовСКонтрагентом КАК Заявка,
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.СтатусОплаты КАК Статус,
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.Сумма КАК СуммаЗаяв,
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.Контрагент КАК Контрагент,
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.НазначениеПлатежа КАК НазначениеПлатежа,
		|	"""" КАК Статья2,
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
		|	"""" КАК Статья1
		|ИЗ
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата КАК ВТ_ЗаявкиПоКоторымПроизведенаОплата
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ОплаченныеЭтапыРабот.ДокументРасчетовСКонтрагентом,
		|	ВТ_ОплаченныеЭтапыРабот.СтатусОплаты,
		|	ВТ_ОплаченныеЭтапыРабот.СуммаКОплате,
		|	ВТ_ОплаченныеЭтапыРабот.Контрагент,
		|	ВТ_ОплаченныеЭтапыРабот.НазначениеПлатежа,
		|	"""",
		|	ВТ_ОплаченныеЭтапыРабот.СтатьяДвиженияДенежныхСредств,
		|	""""
		|ИЗ
		|	ВТ_ОплаченныеЭтапыРабот КАК ВТ_ОплаченныеЭтапыРабот
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗаявкиИСчетаНаОплату.ДокументРасчетовСКонтрагентом,
		|	""Неопределено"",
		|	ВТ_ЗаявкиИСчетаНаОплату.СуммаКОплате,
		|	ВТ_ЗаявкиИСчетаНаОплату.Контрагент,
		|	ВТ_ЗаявкиИСчетаНаОплату.НазначениеПлатежа,
		|	"""",
		|	ВТ_ЗаявкиИСчетаНаОплату.СтатьяДвиженияДенежныхСредств,
		|	""""
		|ИЗ
		|	ВТ_ЗаявкиИСчетаНаОплату КАК ВТ_ЗаявкиИСчетаНаОплату
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОплаченныеЭтапыРабот КАК ВТ_ОплаченныеЭтапыРабот
		|		ПО ВТ_ЗаявкиИСчетаНаОплату.ДокументРасчетовСКонтрагентом = ВТ_ОплаченныеЭтапыРабот.ДокументРасчетовСКонтрагентом
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаявкиПоКоторымПроизведенаОплата КАК ВТ_ЗаявкиПоКоторымПроизведенаОплата
		|		ПО ВТ_ЗаявкиИСчетаНаОплату.ДокументРасчетовСКонтрагентом = ВТ_ЗаявкиПоКоторымПроизведенаОплата.ДокументРасчетовСКонтрагентом
		|ГДЕ
		|	ВТ_ЗаявкиПоКоторымПроизведенаОплата.ДокументРасчетовСКонтрагентом ЕСТЬ NULL
		|	И ВТ_ОплаченныеЭтапыРабот.ДокументРасчетовСКонтрагентом ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_БанковскиеСчетаОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ЗаявкиИСчетаНаОплату
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ОплаченныеЭтапыРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_НоменклатурныеГруппыДляПроверки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_НГДР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ЗаявкиПоКоторымПроизведенаОплата";         
		
		СубконтоНГ = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы;  
		
		СубконтоСчета = Новый Массив;
		СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета);
		СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств);//оно же 51_52  
		
		СубконтоРасчетныеДокументы = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами; 
		
		Счет2001 = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство;
		Счет46 = ПланыСчетов.Хозрасчетный.ВыполненныеЭтапыПоНезавершеннымРаботам; 
		
		Счета51_52 = Новый Массив;
		Счета51_52.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
		Счета51_52.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);   
		
		Счет62 = ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками; 
		
		Запрос.УстановитьПараметр("СубконтоНоменклатурныеГруппы", СубконтоНГ);
		Запрос.УстановитьПараметр("ДатаОтчета", ВыборкаРеестр.Дата); 
		Запрос.УстановитьПараметр("Счет2001", Счет2001);
		Запрос.УстановитьПараметр("Ссылка", ВыборкаРеестр.Ссылка);
		Запрос.УстановитьПараметр("Счет46", Счет46);
		Запрос.УстановитьПараметр("Субконто51_52", СубконтоСчета);
		запрос.УстановитьПараметр("Счета51_52", Счета51_52);
		Запрос.УстановитьПараметр("СубконтоРасчетныеДок", СубконтоРасчетныеДокументы);
		Запрос.УстановитьПараметр("Счет62",	Счет62); 
		
		РезультатПакет = Запрос.ВыполнитьПакет();         
		
		НомерДокумента = Строка(?(ВыборкаРеестр.НомерРеестра = 0, ВыборкаРеестр.Номер, ВыборкаРеестр.НомерРеестра));
		Номер = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(НомерДокумента);
		
		ОбластьШапка 							= МакетДляПечати.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Дата 			= Формат(ВыборкаРеестр.Дата, "ДЛФ=DD");
		ОбластьШапка.Параметры.НомерРеестра 	= Номер; 
		ОбластьШапка.Параметры.Организация 		= ВыборкаРеестр.Организация;
		
		ДокументРезультат.Вывести(ОбластьШапка);
		
		СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"); 
		
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;  
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, СхемаКомпоновкиДанных.НастройкиПоУмолчанию, ДанныеРасшифровки);
		
		ВнешнийНаборДанных = Новый Структура;
		
		Если Не РезультатПакет[1].Пустой() Тогда    
			
			СчетаОрганизации = РезультатПакет[1].Выгрузить();  
			
			ВнешнийНаборДанных.Вставить("Счета", СчетаОрганизации);	
			
		КонецЕсли; 
		
		Если Не РезультатПакет[10].Пустой() Тогда
			
			ДанныеПоЗаявкам = РезультатПакет[10].Выгрузить(); 
			
			Для Каждого СтрокаЗаявок Из ДанныеПоЗаявкам Цикл
				
				СтрокаНастроек = ТаблицаНастроек.Найти(СтрокаЗаявок.СтатьяДДС, "СтатьяДДС");
				
				Если СтрокаНастроек <> Неопределено Тогда 
					ЗаполнитьЗначенияСвойств(СтрокаЗаявок, СтрокаНастроек);
				КонецЕсли;	
				
			КонецЦикла;
			
			ВнешнийНаборДанных.Вставить("ЗаявкиИСтатусыОплат", ДанныеПоЗаявкам);
			
		КонецЕсли;  
		
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);  
		
		ОбластьПодвал = МакетДляПечати.ПолучитьОбласть("ПодвалВНИИБТ");
		
		ДолжностьФИО = ПолучитьГенДиректора(ВыборкаРеестр.Организация);
		
		ОбластьПодвал.Параметры.ДолжностьДляПечати 	= ДолжностьФИО.Должность;
		ОбластьПодвал.Параметры.ФИО 				= ДолжностьФИО.ФИО;
		ОбластьПодвал.Параметры.Период 				= Формат(ВыборкаРеестр.Дата, "ДФ=dd.MM.yyyy");
		
		ДокументРезультат.Вывести(ОбластьПодвал); 
		
		Если МассивОбъектов.Количество() > 1 Тогда 
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументРезультат; 
	
КонецФункции   

Функция ПолучитьГенДиректора(Организация)
	
	Должность = Справочники.Должности.НайтиПоНаименованию("Генеральный директор");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо.Представление КАК ФизЛицо,
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность.Представление КАК Должность
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|	И ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность = &ТекущаяДолжность";
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
	Запрос.УстановитьПараметр("ТекущаяДолжность", Должность);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Структура = Новый Структура;
	Структура.Вставить("Должность", "Генеральный директор");
	
	Если Не Результат.Пустой() Тогда 
		
		Пока Выборка.Следующий() Цикл  
			
			Структура.Вставить("ФИО", Выборка.Физлицо);
			
		КонецЦикла;
		
	Иначе
		Структура.Вставить("ФИО", "");		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

Функция ПолучитьТаблицуНастроек() Экспорт
	
	ТипЧисло 		= Новый ОписаниеТипов("Число", 		Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	ТипСтрока 		= Новый ОписаниеТипов("Строка", , 	Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная));
	ТипДата  		= Новый ОписаниеТипов("Дата", , , 	Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	
	ТаблицаНастроек.Колонки.Добавить("Порядок", 		ТипЧисло);
	ТаблицаНастроек.Колонки.Добавить("Статья1", 		ТипСтрока);
	ТаблицаНастроек.Колонки.Добавить("Статья2",			ТипСтрока);
	ТаблицаНастроек.Колонки.Добавить("Статья3",			ТипСтрока);
	ТаблицаНастроек.Колонки.Добавить("СтатьяДДС",		Новый ОписаниеТипов("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	ТаблицаНастроек.Индексы.Добавить("СтатьяДДС");
	
	// Считываем настройки для сбора информации
	// Сначала считываем индивидуальные настройки по конкретным статьям
	Макет = ПолучитьМакет("Настройки");  
	
	Для Счетчик = 2 По 100 Цикл
		
		Колонка1 = Макет.ПолучитьОбласть("R" + Счетчик + "C1").ТекущаяОбласть.Текст;
		Если НЕ ЗначениеЗаполнено(Колонка1) Тогда
			Прервать;
		КонецЕсли; 
		
		Колонка2 = Макет.ПолучитьОбласть("R" + Счетчик + "C2").ТекущаяОбласть.Текст;
		Колонка3 = Макет.ПолучитьОбласть("R" + Счетчик + "C3").ТекущаяОбласть.Текст;
		Колонка4 = Макет.ПолучитьОбласть("R" + Счетчик + "C4").ТекущаяОбласть.Текст;
		Колонка5 = Макет.ПолучитьОбласть("R" + Счетчик + "C5").ТекущаяОбласть.Текст;
		Колонка6 = Макет.ПолучитьОбласть("R" + Счетчик + "C6").ТекущаяОбласть.Текст;
		Колонка7 = Макет.ПолучитьОбласть("R" + Счетчик + "C7").ТекущаяОбласть.Текст;
		
		Если НЕ ЗначениеЗаполнено(Колонка7) Тогда
			Продолжить;
		КонецЕсли; 
		
		СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьСсылку(Новый УникальныйИдентификатор(Колонка7));
		
		Если Не СтатьяДДС.ЭтоГруппа Тогда
			
			НоваяСтрока 			= ТаблицаНастроек.Добавить();	
			НоваяСтрока.Статья1 	= Колонка1;
			НоваяСтрока.Статья2 	= Колонка2;
			НоваяСтрока.Статья3 	= Колонка3;
			НоваяСтрока.СтатьяДДС 	= СтатьяДДС;
			НоваяСтрока.Порядок		= Число(Колонка6);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Затем по группам, т.к. отдельные элементы могут входить в 
	// другие группы
	Для Счетчик = 2 По 100 Цикл
		
		Колонка1 = Макет.ПолучитьОбласть("R" + Счетчик + "C1").ТекущаяОбласть.Текст; 
		
		Если НЕ ЗначениеЗаполнено(Колонка1) Тогда
			Прервать;
		КонецЕсли; 
		
		Колонка2 = Макет.ПолучитьОбласть("R" + Счетчик + "C2").ТекущаяОбласть.Текст;
		Колонка3 = Макет.ПолучитьОбласть("R" + Счетчик + "C3").ТекущаяОбласть.Текст;
		Колонка4 = Макет.ПолучитьОбласть("R" + Счетчик + "C4").ТекущаяОбласть.Текст;
		Колонка5 = Макет.ПолучитьОбласть("R" + Счетчик + "C5").ТекущаяОбласть.Текст;
		Колонка6 = Макет.ПолучитьОбласть("R" + Счетчик + "C6").ТекущаяОбласть.Текст;
		Колонка7 = Макет.ПолучитьОбласть("R" + Счетчик + "C7").ТекущаяОбласть.Текст;
		
		Если НЕ ЗначениеЗаполнено(Колонка7) Тогда
			Продолжить;
		КонецЕсли; 
		
		ГруппаДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьСсылку(Новый УникальныйИдентификатор(Колонка7));
		Если Не ГруппаДДС.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		// Для каждой статьи ДДС назначаем соответствие
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатьиДвиженияДенежныхСредств.Ссылка
			|ИЗ
			|	Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДвиженияДенежныхСредств
			|ГДЕ
			|	СтатьиДвиженияДенежныхСредств.Ссылка В ИЕРАРХИИ(&ГруппаДДС)
			|	И НЕ СтатьиДвиженияДенежныхСредств.ЭтоГруппа";
		
		Запрос.УстановитьПараметр("ГруппаДДС", ГруппаДДС); 
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать(); 
		
		Пока Выборка.Следующий() Цикл
			
			Если ТаблицаНастроек.Найти(Выборка.Ссылка, "СтатьяДДС") <> Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			НоваяСтрока 			= ТаблицаНастроек.Добавить();	
			НоваяСтрока.Статья1 	= Колонка1;
			НоваяСтрока.Статья2 	= Колонка2;
			НоваяСтрока.Статья3 	= Колонка3;
			НоваяСтрока.СтатьяДДС 	= Выборка.Ссылка;
			НоваяСтрока.Порядок		= Число(Колонка6);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаНастроек;
	
КонецФункции 

Функция ПроверитьЗаполнениеТЧДокументов(МассивПроверки) 
	
	МассивОбъектов = Новый Массив;
	
	Для Каждого Строка Из МассивПроверки Цикл 
		
		Документ = Строка.Ссылка;
		Сводные = Документ.СводныеОстатки;
		
		Если Сводные.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнена таблица ""Сводные остатки"" в документе - " + Документ.Номер + " !"); 
		Иначе
			МассивОбъектов.Добавить(Строка);
		КонецЕсли; 
		
	КонецЦикла; 
	
	Возврат МассивОбъектов;
	
КонецФункции

