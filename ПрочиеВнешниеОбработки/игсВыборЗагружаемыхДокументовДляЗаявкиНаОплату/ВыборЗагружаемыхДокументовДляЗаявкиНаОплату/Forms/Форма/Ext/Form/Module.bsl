
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Устанавливается значение тумблера по умолчанию
	Объект.ИсточникФайлов = "ДО";
	
	ИзменитьДоступностьКнопкиПомещения(Ложь);
	
	Если Параметры.Свойство("ОбъектыНазначения") Тогда 
		
		Объект.ЗРДС = Параметры.ОбъектыНазначения[0]; 
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ЗРДС", "Доступность", Ложь); 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

// Открывает файлы при двойном нажатии на строку
&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеФайла = Элементы.Файлы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ДанныеФайла.ДанныеФайлаBase64) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		ДвоичныеДанные = Base64Значение(ДанныеФайла.ДанныеФайлаBase64);
		ИмяФайла = ПолучитьИмяВременногоФайла(ДанныеФайла.Расширение);
		ДвоичныеДанные.Записать(ИмяФайла);
		ЗапуститьПриложение(ИмяФайла);
		
	Исключение
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		"При попытке открыть файл возникла ошибка. Описание: " + ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Вызывает заполнение ТЧ "Файлы" в зависимости от тумблера и от заполненности ТЧ "ТабличнаяЧастьЗаявки" 
&НаКлиенте
Процедура ПолучитьФайлыСмежныхСистем(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ЗРДС) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		"Заявка на оплату должна быть указана!"
		, , "ЗРДС", "Объект");
		Возврат;
	КонецЕсли;
	
	Если Объект.ИсточникФайлов = "ДО: Договорной документ" Тогда	
		ПолучитьДоговорДО();
		Возврат;
	КонецЕсли; 

	// Если не найден ни один документ ПТиУ в ДокументыРасчетов у ЗРДС
	Если НЕ АктуализироватьТабличнуюЧастьЗаявки() Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		"У текущей Заявки на оплату отсутствуют Документы расчета типа ""Поступление товаров и услуг""!");
		Возврат;
		
	КонецЕсли;
	
	Для Каждого ДокументРасчета Из Объект.ТабличнаяЧастьЗаявки Цикл
		
		Если Объект.ИсточникФайлов = "ДО" Тогда
			ПолучитьДО(ДокументРасчета.Ссылка, ДокументРасчета.НомерЛСЗ, ДокументРасчета.СуммаДокумента);
		ИначеЕсли Объект.ИсточникФайлов = "МТО (ERP)" Тогда
			ПолучитьМТО(ДокументРасчета.Ссылка, ДокументРасчета.ГУИД);
		ИначеЕсли Объект.ИсточникФайлов = "ЭДО" Тогда
			ПолучитьЭДО(ДокументРасчета.Ссылка, ДокументРасчета.ДатаВходящегоДокумента, 
			ДокументРасчета.НомерВходящегоДокумента, ДокументРасчета.ИНН, ДокументРасчета.СуммаДокумента);
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВПрисоединенныеФайлы(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ЗРДС) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		"Заявка на оплату должна быть указана!"
		, , "ЗРДС", "Объект");
		Возврат;
	КонецЕсли;
	
	ЗагрузитьВПрисоединенныеФайлыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГалочки(Команда)
	
	УправлениеГалочками(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьГалочки(Команда)
	
	УправлениеГалочками(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает файлы из ДО по данным ПТиУ
&НаСервере
Процедура ПолучитьДО(СсылкаПТиУ, НомерЛСЗ, СуммаДокумента)
	
	ИдентификаторИсточника = Объект.ИсточникФайлов;
	ОчисткаПредыдущихФайлов(ИдентификаторИсточника);
	
	Попытка
		
		ПараметрыСервиса = Новый Структура;
		ПараметрыСервиса.Вставить("НомерЛСЗ", НомерЛСЗ);
		ПараметрыСервиса.Вставить("Сумма", СуммаДокумента);
		
		Результат = игсПрисоединеныеФайлыВнешнихСистемСервер.СписокФайловДОСДвоичнымиДанными(ПараметрыСервиса);
		
		Если Результат.ТаблицаФайлов.Количество() = 0 Тогда
			
			Результат.Ошибки.Добавить("Не найдено вложенных файлов для документа " + Строка(СсылкаПТиУ));	
			
		КонецЕсли;
		
		Если Результат.Ошибки.Количество() > 0 Тогда
			
			ВывестиСписокОшибокСервиса(ИдентификаторИсточника, Результат.Ошибки);
			Возврат;
			
		КонецЕсли; 
		
		Для Каждого Элемент Из Результат.ТаблицаФайлов Цикл
			
			СтрокаТаблицыФайлов = Объект.Файлы.Добавить();
			СтрокаТаблицыФайлов.Источник = ИдентификаторИсточника;
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыФайлов, Элемент); 	
			
		КонецЦикла;
			
	Исключение
		
		ВывестиОшибкуПолученияФайла(ИдентификаторИсточника, ОписаниеОшибки()); 
		Возврат;
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает файлы из ERP (МТО) по данным ПТиУ
&НаСервере
Процедура ПолучитьМТО(СсылкаПТиУ, ГУИД)
	
	ИдентификаторИсточника = Объект.ИсточникФайлов;
	ОчисткаПредыдущихФайлов(ИдентификаторИсточника);
	
	Попытка
		
		ПараметрыСервиса = Новый Структура;
		ПараметрыСервиса.Вставить("Идентификатор", ГУИД);
		
		Результат = игсПрисоединеныеФайлыВнешнихСистемСервер.СписокФайловЕРП(ПараметрыСервиса);
		
		Если Результат.Ошибки.Количество() > 0 Тогда
			
			ВывестиСписокОшибокСервиса(ИдентификаторИсточника, Результат.Ошибки);
			Возврат;      
			
		КонецЕсли; 
		
		Для Каждого Элемент Из Результат.ТаблицаФайлов Цикл
			
			СтрокаТаблицыФайлов = Объект.Файлы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыФайлов, Элемент); 
			СтрокаТаблицыФайлов.ДанныеФайлаBase64 = Base64Строка(игсПрисоединеныеФайлыВнешнихСистемСервер.GetFile(Элемент.Код, "ERP"));
			СтрокаТаблицыФайлов.Источник = ИдентификаторИсточника;
			
		КонецЦикла;
			
	Исключение
		
		ВывестиОшибкуПолученияФайла(ИдентификаторИсточника, ОписаниеОшибки()); 
		Возврат;
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает накладную из ЭДО по данным ПТиУ
&НаСервере
Процедура ПолучитьЭДО(СсылкаПТиУ, ДатаВходящегоДокумента, НомерВходящегоДокумента, ИНН, СуммаДокумента)
	
	ИдентификаторИсточника = Объект.ИсточникФайлов;
	ОчисткаПредыдущихФайлов(ИдентификаторИсточника);
	
	ПараметрыСервиса = Новый Структура;
		
	ПараметрыСервиса.Вставить("Дата", ДатаВходящегоДокумента);
	ПараметрыСервиса.Вставить("Номер", НомерВходящегоДокумента);
	ПараметрыСервиса.Вставить("ИНН", ИНН);
	ПараметрыСервиса.Вставить("СуммаДокумента", СуммаДокумента);

	Попытка
		
		ДвоичныеДанные = игсПрисоединеныеФайлыВнешнихСистемСервер.ФормаАктаИзЭДО_ЗРДС(ПараметрыСервиса, Ложь, Истина);
		
		Если ДвоичныеДанные = Неопределено Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
			"В ЭДО не удалось найти накладной для документа """ + Строка(СсылкаПТиУ) + """!");
			Возврат;
			
		КонецЕсли;
		
		ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
		
		ДвоичныеДанные.Записать(ИмяВремФайла);
			
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяВремФайла);
		
		Сериализатор     = Новый СериализаторXDTO(ФабрикаXDTO);

		ПечатнаяФорма = СериализаторXDTO.ПрочитатьXML(ЧтениеXML, Тип("Структура"));
		
		Если ПечатнаяФорма.Свойство("ПустаяПечатнаяФорма") 
			И ПечатнаяФорма.ПустаяПечатнаяФорма Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
			"Для документа """ + Строка(СсылкаПТиУ) + """ отсутствует накладная в ЭДО!");
			Возврат;
			
		КонецЕсли;
		
		ЧтениеXML.Закрыть();
		
		НоваяСтрока = Объект.Файлы.Добавить();
		НоваяСтрока.Выбор = Ложь;
		НоваяСтрока.Наименование = ПечатнаяФорма.Представление;
		НоваяСтрока.Расширение = "pdf";
		НоваяСтрока.ДанныеФайлаBase64 = Base64Строка(ПечатнаяФорма.ПечатнаяФормаДвоичныеДанные);
		НоваяСтрока.Источник = ИдентификаторИсточника;
		
	Исключение
		
		ВывестиОшибкуПолученияФайла(ИдентификаторИсточника, ОписаниеОшибки()); 
		Возврат;
		
	КонецПопытки;
КонецПроцедуры

// Возвращает файлы из ДО по данным Договору Контрагента из ЗРДС
&НаСервере
Процедура ПолучитьДоговорДО()
	
	ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗРДС, "ДоговорКонтрагента");
	
	ИдентификаторИсточника = Объект.ИсточникФайлов;
	ОчисткаПредыдущихФайлов(ИдентификаторИсточника);
	
	ПараметрыСервиса = Новый Структура;	
	ПараметрыСервиса.Вставить("Идентификатор", ДоговорКонтрагента.УникальныйИдентификатор());
	
	Попытка
		
		Результат = игсПрисоединеныеФайлыВнешнихСистемСервер.СписокФайловДОСДвоичнымиДанными(ПараметрыСервиса);
		
		Если Результат.ТаблицаФайлов.Количество() = 0 Тогда
			
			Результат.Ошибки.Добавить("Не найдено вложенных файлов для договора """ + Строка(ДоговорКонтрагента) + """");	
			
		КонецЕсли;
		
		Если Результат.Ошибки.Количество() > 0 Тогда
			
			ВывестиСписокОшибокСервиса(ИдентификаторИсточника, Результат.Ошибки);
			Возврат;
			
		КонецЕсли; 
		
		Для Каждого Элемент Из Результат.ТаблицаФайлов Цикл
			
			СтрокаТаблицыФайлов = Объект.Файлы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыФайлов, Элемент);
			СтрокаТаблицыФайлов.Источник = ИдентификаторИсточника;
			
		КонецЦикла;
			
	Исключение
		
		ВывестиОшибкуПолученияФайла(ИдентификаторИсточника, ОписаниеОшибки()); 
		Возврат;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВПрисоединенныеФайлыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.Ссылка КАК Ссылка,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.Наименование КАК Наименование,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.Том КАК Том,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.ПутьКФайлу КАК ПутьКФайлу,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.ФайлХранилище КАК ФайлХранилище,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.ТекстХранилище КАК ТекстХранилище,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.ТипХраненияФайла КАК ТипХраненияФайла,
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.Расширение КАК Расширение
			|ИЗ
			|	Справочник.игсЗаявкаНаОплатуПрисоединенныеФайлы КАК игсЗаявкаНаОплатуПрисоединенныеФайлы
			|ГДЕ
			|	игсЗаявкаНаОплатуПрисоединенныеФайлы.ВладелецФайла = &ЗРДС
			|	И НЕ игсЗаявкаНаОплатуПрисоединенныеФайлы.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ЗРДС", Объект.ЗРДС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Проверка на уже существующий файл будет путем сравнивая Хеш-Сумм
	ХешСуммыMD5Файлов = Новый Соответствие;
			
	Пока Выборка.Следующий() Цикл
		
		ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(Выборка.Ссылка); 
		
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
		ХешированиеДанных.Добавить(ДвоичныеДанные);
		
		ХешСуммыMD5Файлов.Вставить(
			Base64Строка(ХешированиеДанных.ХешСумма),
			Новый Структура("Ссылка, Наименование, ИмяФайла", Выборка.Ссылка, 
			Выборка.Наименование, Выборка.Наименование + "." + Выборка.Расширение));
		
	КонецЦикла;
	
	Для каждого СтрокаТаблицы из Объект.Файлы Цикл
		Если СтрокаТаблицы.Выбор Тогда
			ДвоичныеДанные = Base64Значение(СтрокаТаблицы.ДанныеФайлаBase64);
			
			АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			
			ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
			ХешированиеДанных.Добавить(ДвоичныеДанные);
			MD5Файла = Base64Строка(ХешированиеДанных.ХешСумма);
			
			НайденныйФайл = ХешСуммыMD5Файлов[MD5Файла];
			
			Если НайденныйФайл = Неопределено Тогда
				
				ПараметрыФайла = Новый Структура;
				ПараметрыФайла.Вставить("ВладелецФайлов", Объект.ЗРДС);
				ПараметрыФайла.Вставить("ИмяБезРасширения", СтрокаТаблицы.Наименование + СтрШаблон(" (%1)", СтрокаТаблицы.Источник));
				ПараметрыФайла.Вставить("РасширениеБезТочки", СтрокаТаблицы.Расширение);
				ПараметрыФайла.Вставить("Автор", Пользователи.ТекущийПользователь());
				ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", ТекущаяДатаСеанса());
				
				ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресВХранилище);
				
				СтрокаТаблицы.Выбор = Ложь;	
				
				ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон("Успех: Файл ""%1.%2"" успешно загружен! Признак выбора в таблице был снят.", 
				СтрокаТаблицы.Наименование, СтрокаТаблицы.Расширение));
				
			Иначе
				
				ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон("Ошибка: Файл ""%1.%2"" ранее был загружен! Выберите к загрузке другой файл, либо удалите ранее загруженный.
				|Ранее загруженный файл в ИБ: %3", 
				СтрокаТаблицы.Наименование, СтрокаТаблицы.Расширение, НайденныйФайл.Наименование));
				
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеГалочками(Установить)

	Для Каждого Элемент Из Объект.Файлы Цикл
		
		Элемент.Выбор = Установить;
		
	КонецЦикла; 
	
	ИзменитьДоступностьКнопкиПомещения(Установить);
	
КонецПроцедуры   

// Очищает список по указанному ключу колонки "Источник"
&НаСервере
Процедура ОчисткаПредыдущихФайлов(Источник)
	
	КоличествоСтрок = Объект.Файлы.Количество() - 1; 
	ИндексСтроки = КоличествоСтрок; 

	Для Индекс = 0 по КоличествоСтрок Цикл 
	 	Запись = Объект.Файлы.Получить(ИндексСтроки); 
		Если Запись.Источник = Источник тогда 
			Объект.Файлы.Удалить(Запись); 
		КонецЕсли; 
		ИндексСтроки = ИндексСтроки - 1; 
	КонецЦикла; 
	
КонецПроцедуры

// Возвращает наличие документов ПТиУ у ЗРДС и заполняет ими ТЧ "ТабличнаяЧастьЗаявки"
&НаСервере
Функция АктуализироватьТабличнуюЧастьЗаявки()
	
	Объект.ТабличнаяЧастьЗаявки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом КАК Ссылка,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ПоступлениеТоваровУслуг.Ссылка) КАК ГУИД,
			|	ПоступлениеТоваровУслуг.игсНомерЛСЗ КАК НомерЛСЗ,
			|	ПоступлениеТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
			|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
			|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
			|	Контрагенты.ИНН КАК ИНН
			|ИЗ
			|	Документ.игсЗаявкаНаОплату.ДокументыРасчетов КАК игсЗаявкаНаОплатуДокументыРасчетов
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
			|			ПО ПоступлениеТоваровУслуг.Контрагент = Контрагенты.Ссылка
			|		ПО игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом = ПоступлениеТоваровУслуг.Ссылка
			|ГДЕ
			|	игсЗаявкаНаОплатуДокументыРасчетов.Ссылка = &СсылкаЗРДС
			|	И ТИПЗНАЧЕНИЯ(игсЗаявкаНаОплатуДокументыРасчетов.ДокументРасчетовСКонтрагентом) = ТИП(Документ.ПоступлениеТоваровУслуг)";
	
	Запрос.УстановитьПараметр("СсылкаЗРДС", Объект.ЗРДС);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
		
	Пока Выборка.Следующий() Цикл  
		
		НоваяСтрока = Объект.ТабличнаяЧастьЗаявки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка); 
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Возвращает булево - выбран ли хотя бы один файл из тч "Файлы" с заполненными данными
&НаКлиенте
Процедура ФайлыВыборПриИзменении(Элемент)
	
	ВыбранЭлемент = Ложь;
	
	Для Каждого Элемент Из Объект.Файлы Цикл
		
		Если Элемент.Выбор И ЗначениеЗаполнено(Элемент.ДанныеФайлаBase64) Тогда
			
			ВыбранЭлемент = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИзменитьДоступностьКнопкиПомещения(ВыбранЭлемент);
	
КонецПроцедуры

// Выводит массив ошибок 
&НаСервере
Процедура ВывестиСписокОшибокСервиса(ИдентификаторИсточника, СписокОшибок)

	СообщениеОбОшибке = СтрШаблон("Не удалось получить вложенные файлы из %1. Список ошибок: ", ИдентификаторИсточника);
			
	Индекс = 1;
	
	Для Каждого Ошибка Из СписокОшибок Цикл
		
		СообщениеОбОшибке = СообщениеОбОшибке + СтрШаблон("%1. %2. ", Индекс, Ошибка);
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке);
	
КонецПроцедуры

// Выводит типовой текст ошибки о невозможности получить файл
&НаСервере
Процедура ВывестиОшибкуПолученияФайла(ИдентификаторИсточника, ОписаниеОшибки)

	ОбщегоНазначения.СообщитьПользователю(
	СтрШаблон("Возникли проблемы при попытке получения файлов из %1! Описание ошибки: " + ОписаниеОшибки, ИдентификаторИсточника));	
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьКнопкиПомещения(Значение)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтаФорма.Элементы, 
	"ЗагрузитьВПрисоединенныеФайлы", "Доступность", Значение);	
	
КонецПроцедуры

#КонецОбласти

