
#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	
	// Заполнение реквизитов
	Объект.Дата = ТекущаяДата();
	Объект.Организация = ПолучитьОрганизациюПоУмолчанию(); 
	
КонецПроцедуры 

// Проверка заполненности полей и последующее выполнение команды на сервере 
&НаКлиенте
Асинх Процедура ВыполнитьДействие(Команда)
	
	// ОперацияБух заполнена, уточнение у пользователя
	Если ЗначениеЗаполнено(Объект.ОперацияБух) Тогда
		
		ОсновныеДанные = ПолучитьДатуОрганизациюОперацииБух(Объект.ОперацияБух);	
		
		Обещание = ВопросАсинх(СтрШаблон("Это действие очистит ВСЕ движения этого документа!
		|
		|Будут использованы реквизиты из этого документа: 
		| - Дата: %1;
		| - Организация: %2.
		|
		|Продолжить?",
		ОсновныеДанные.Дата,
		ОсновныеДанные.Организация,),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Результат = Ждать Обещание;
		Если Результат <> КодВозвратаДиалога.Да Тогда 
			
			Возврат;
			
		КонецЕсли;
		
	// ОперацияБух не заполнена, Организация и Дата должны быть заполнены
	Иначе
	
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда 
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо указать организацию!", , "Организация", "Объект",);
			
			Возврат;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда 
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо указать дату!", , "Дата", "Объект",);                     
			
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	ВыполнитьДействиеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает организацию пользователя
&НаСервереБезКонтекста
Функция ПолучитьОрганизациюПоУмолчанию()

	Возврат ОбщегоНазначения.ОбщийМодуль("Справочники.Организации").ОрганизацияПоУмолчанию();
	
КонецФункции

// Создание/Изменение документа ОперацииБух
&НаСервере
Процедура ВыполнитьДействиеНаСервере()
	
	Попытка
		
		НачалоСообщения = ?(ЗначениеЗаполнено(Объект.ОперацияБух), "Документ изменен", "Создан новый документ");
		
		ОбъектОбработки = РеквизитФормыВЗначение("Объект");
		ДокументОперацииБух = ОбъектОбработки.СоздатьИзменитьОперацияБух();
		Объект.ОперацияБух = ДокументОперацииБух;
		
		// В зависимости от результата функции, пользователь получает разные сообщения
		Если ЗначениеЗаполнено(ДокументОперацииБух) Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон("%1: ""Операция %2"", от %3; Нажмите на это сообщение чтобы открыть документ.", 
			НачалоСообщения, ДокументОперацииБух.Ссылка.Номер, Формат(ДокументОперацииБух.Дата,"ДФ=dd.MM.yyyy")), ДокументОперацииБух);
				
		Иначе
			
			ОбщегоНазначения.СообщитьПользователю(
			"Не найдено закрытых проектов, требующих перераспределения! Пожалуйста, перепроверьте параметры.");
			
		КонецЕсли;
		
	Исключение
		
		ОбщегоНазначения.СообщитьПользователю(
		"Не удалось создать документ Операции. Причина: " + ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает структуру с Датой и Организацией указанной ОперацииБух
&НаСервереБезКонтекста
Функция ПолучитьДатуОрганизациюОперацииБух(ОперацияБухСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОперацияБух.Дата КАК Дата,
	|	ОперацияБух.Организация КАК Организация
	|ИЗ
	|	Документ.ОперацияБух КАК ОперацияБух
	|ГДЕ
	|	ОперацияБух.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ОперацияБухСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Возвращаемая структура
	ОсновныеДанные = Новый Структура;
	
	ОсновныеДанные.Вставить("Дата",			"не заполнена");
	ОсновныеДанные.Вставить("Организация",	"не заполнена");
	
	Если Выборка.Следующий() Тогда
		ОсновныеДанные.Дата = ?(ЗначениеЗаполнено(Выборка.Дата), Строка(Выборка.Дата), "не заполнена");
		ОсновныеДанные.Организация = ?(ЗначениеЗаполнено(Выборка.Организация), Строка(Выборка.Организация), "не заполнена");		
	КонецЕсли;
	
	Возврат ОсновныеДанные;
	
КонецФункции

#КонецОбласти