
Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид","ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение",ПолучитьНазначениеОбработки());
	ПараметрыРегистрации.Вставить("Наименование","Перемещение арендованных ОС");  
	ПараметрыРегистрации.Вставить("Информация","Перемещение арендованных ОС");
	ПараметрыРегистрации.Вставить("БезопасныйРежим",Ложь);
	
	Команды = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(Команды,"Перемещение арендованных ОС", "ПеремещениеАрендованныхОС", "ВызовСерверногоМетода", Ложь, "ПечатьMXL");
	
	ПараметрыРегистрации.Вставить("Команды",Команды);   
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьНазначениеОбработки()
	
	Массив = Новый Массив;
	Массив.Добавить("Документ.игсУчетЗабалансовыхОС"); 
	
	Возврат Массив; 
	
КонецФункции        

Функция ПолучитьТаблицуКоманд() 
	
	Команды = 											Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", 			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", 			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", 			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", 	Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", 			Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "") 
	
	НоваяКоманда 						= ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление 			= Представление;
	НоваяКоманда.Идентификатор 			= Идентификатор;
	НоваяКоманда.Использование 			= Использование;
	НоваяКоманда.ПоказыватьОповещение 	= ПоказыватьОповещение;
	НоваяКоманда.Модификатор 			= Модификатор;
	
КонецПроцедуры  

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПеремещениеАрендованныхОС", "Перемещение арендованных ОС", ПечатьОбъекта(МассивОбъектов));
	
КонецПроцедуры

Функция ПечатьОбъекта(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПеремещениеОС.Дата КАК ДатаДок,
		|	ПеремещениеОС.Номер КАК НомерДок,
		|	ПеремещениеОС.Ответственный КАК Ответственный,
		|	ПеремещениеОС.Организация КАК Организация,
		|	ПеремещениеОС.Дата КАК ДатаПередачи,
		|	Организации.КодПоОКПО КАК КодПоОКПО,
		|	ПеремещениеОС.Ссылка КАК Ссылка,
		|	Организации.Представление КАК ОрганизацияП
		|ПОМЕСТИТЬ ВтДокументы
		|ИЗ
		|	Документ.игсУчетЗабалансовыхОС КАК ПеремещениеОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ПеремещениеОС.Организация = Организации.Ссылка
		|ГДЕ
		|	ПеремещениеОС.Ссылка В(&Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВтДокументы.ДатаДок КАК ДатаДок,
		|	ВтДокументы.НомерДок КАК НомерДок,
		|	ВтДокументы.Ответственный КАК Ответственный,
		|	ВтДокументы.Организация КАК Организация,
		|	ВтДокументы.ДатаПередачи КАК ДатаПередачи,
		|	ВтДокументы.КодПоОКПО КАК КодПоОКПО,
		|	ВтДокументы.Ссылка КАК Ссылка,
		|	ВтДокументы.ОрганизацияП КАК ОрганизацияП,
		|	игсУчетЗабалансовыхОСПриходОС.Склад КАК СкладПриход,
		|	игсУчетЗабалансовыхОСПриходОС.Сумма КАК СуммаПриход,
		|	игсУчетЗабалансовыхОСПриходОС.ОсновноеСредство КАК ОсновноеСредствоПриход,
		|	ПодразделенияОрганизаций.Представление КАК ПолучательП,
		|	Склады.Представление КАК СкладПриходП
		|ПОМЕСТИТЬ ВТПеремещениеОСПриход
		|ИЗ
		|	ВтДокументы КАК ВтДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.игсУчетЗабалансовыхОС.ПриходОС КАК игсУчетЗабалансовыхОСПриходОС
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|			ПО игсУчетЗабалансовыхОСПриходОС.Склад = Склады.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|			ПО игсУчетЗабалансовыхОСПриходОС.Подразделение = ПодразделенияОрганизаций.Ссылка
		|		ПО ВтДокументы.Ссылка = игсУчетЗабалансовыхОСПриходОС.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВтДокументы.Ссылка КАК СсылкаРасход,
		|	игсУчетЗабалансовыхОСРасходОС.Склад КАК СкладРасход,
		|	игсУчетЗабалансовыхОСРасходОС.Сумма КАК СуммаРасход,
		|	игсУчетЗабалансовыхОСРасходОС.ОсновноеСредство КАК ОсновноеСредствоРасход,
		|	ПодразделенияОрганизаций.Представление КАК ОтправительП,
		|	Склады.Представление КАК СкладРасходП
		|ПОМЕСТИТЬ ВТПеремещениеОСрасход
		|ИЗ
		|	ВтДокументы КАК ВтДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.игсУчетЗабалансовыхОС.РасходОС КАК игсУчетЗабалансовыхОСРасходОС
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|				ПО Склады.ПодразделениеОрганизации = ПодразделенияОрганизаций.Ссылка
		|			ПО игсУчетЗабалансовыхОСРасходОС.Склад = Склады.Ссылка
		|		ПО ВтДокументы.Ссылка = игсУчетЗабалансовыхОСРасходОС.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПеремещениеОСПриход.Ссылка КАК Ссылка,
		|	ВТПеремещениеОСПриход.ДатаДок КАК ДатаДок,
		|	ВТПеремещениеОСПриход.НомерДок КАК НомерДок,
		|	ВТПеремещениеОСПриход.Ответственный КАК Ответственный,
		|	ВТПеремещениеОСПриход.Организация КАК Организация,
		|	ВТПеремещениеОСПриход.ДатаПередачи КАК ДатаПередачи,
		|	ВТПеремещениеОСПриход.КодПоОКПО КАК КодПоОКПО,
		|	ВТПеремещениеОСПриход.ОрганизацияП КАК ОрганизацияП,
		|	ВТПеремещениеОСПриход.ОсновноеСредствоПриход КАК ОсновноеСредство,
		|	ВТПеремещениеОСПриход.СуммаПриход КАК СуммаПеремещения,
		|	ВТПеремещениеОСПриход.ПолучательП КАК ПодрПолучатель,
		|	ВТПеремещениеОСПриход.СкладПриходП КАК СкладПриходП,
		|	ВТПеремещениеОСрасход.ОтправительП КАК ПодрСдатчик,
		|	ВТПеремещениеОСрасход.СкладРасходП КАК СкладРасходП,
		|	ВТПеремещениеОСПриход.СкладПриход КАК СкладПриход,
		|	ВТПеремещениеОСрасход.СкладРасход КАК СкладРасход,
		|	ВТПеремещениеОСПриход.ОсновноеСредствоПриход.Код КАК ИнвНомер,
		|	ВТПеремещениеОСПриход.ОсновноеСредствоПриход.НаименованиеПолное КАК НаименованиеОС,
		|	ВТПеремещениеОСПриход.ОсновноеСредствоПриход.ДатаВыпуска КАК ГодВыпуска
		|ИЗ
		|	ВТПеремещениеОСПриход КАК ВТПеремещениеОСПриход
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПеремещениеОСрасход КАК ВТПеремещениеОСрасход
		|		ПО ВТПеремещениеОСПриход.Ссылка = ВТПеремещениеОСрасход.СсылкаРасход
		|			И ВТПеремещениеОСПриход.ОсновноеСредствоПриход = ВТПеремещениеОСрасход.ОсновноеСредствоРасход
		|			И ВТПеремещениеОСПриход.СкладПриход <> ВТПеремещениеОСрасход.СкладРасход
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПеремещениеОСПриход
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПеремещениеОСрасход";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов);
	
	ТаблицаПоОС = Запрос.Выполнить().Выгрузить();
	
	ТабДокумент   = Новый ТабличныйДокумент();
	
	Для Каждого Элемент Из МассивОбъектов Цикл
		ВывестиДокумент(Элемент, ТаблицаПоОС, ТабДокумент)
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции 

Процедура ВывестиДокумент(Документ, ТаблицаПоОС, ТабДокумент)  
	
	ВалютаПечати = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТаблицыОдногоДокумента = Новый Массив; 
	
	Для Каждого Строка из ТаблицаПоОС Цикл
		Если Строка.Ссылка = Документ.Ссылка Тогда
			ТаблицыОдногоДокумента.Добавить(Строка); 
			СкладПриход 	= Строка.СкладПриход;
			СкладРасход 	= Строка.СкладРасход;
			СкладПриходП 	= Строка.СкладПриходП;
			СкладРасходП 	= Строка.СкладРасходП;
			ОрганизацияП 	= Строка.ОрганизацияП;
		КонецЕсли
	КонецЦикла;
	
	ТабДокумент.ПолеСверху         	= 0;
	ТабДокумент.ПолеСлева          	= 0;
	ТабДокумент.ПолеСнизу          	= 0;
	ТабДокумент.ПолеСправа         	= 0;
	ТабДокумент.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	ТабДокумент.Автомасштаб 		= Истина;
	
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеОС_ОС";
	
	Макет                 = ПолучитьМакет("ИГС_ОС2");
	ОбластьШапка1         = Макет.ПолучитьОбласть("Шапка1");
	ОбластьСтрока         = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвалСтраницы = Макет.ПолучитьОбласть("ПодвалСтраницы");
	ОбластьПодвал         = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапка2         = Макет.ПолучитьОбласть("Шапка2");
	
	ОбластьШапка1.Параметры.Валюта = ВалютаПечати;
	ОбластьШапка1.Параметры.Организация = ОрганизацияП;
	
	НС = 1;
	ИтогСумма = 0;
	
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Документ.Организация, Документ.Дата);
	Бухгалтер    = Руководители.ГлавныйБухгалтер; 
	
	ОбластьПодвал.Параметры.ГлавБух = Бухгалтер;
	
	Для Каждого СтрокаМассива Из ТаблицыОдногоДокумента Цикл
		Строка.НомерДок = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Строка.НомерДок);
		ОбластьШапка1.Параметры.Заполнить(СтрокаМассива);
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьШапка1);
	
	ОбластьПодвал.Параметры.ПолучательРасшифровкаПодписи = СкладПриходП;
	
	ОбластьПодвал.Параметры.СдатчикРасшифровкаПодписи = СкладРасходП;
	
	Для Счетчик = 0 По ТаблицыОдногоДокумента.Количество()-1  Цикл
		
		Если НЕ Счетчик = 0 И НЕ ТаблицыОдногоДокумента[Счетчик].СкладПриход = ТаблицыОдногоДокумента[Счетчик-1].СкладПриход Тогда 
			
			ТабДокумент.Вывести(ОбластьПодвал);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ОбластьШапка2);
			
			ОбластьПодвал.Параметры.ПолучательРасшифровкаПодписи = СкладПриход;
			
			ИтогСумма = 0;
			НС = 1; 
			
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(ТаблицыОдногоДокумента[Счетчик]);
		ОбластьСтрока.Параметры.НС = НС; 
		
		Если ПустаяСтрока(ТаблицыОдногоДокумента[Счетчик].НаименованиеОС) Тогда
			ОбластьСтрока.Параметры.НаименованиеОС = СокрЛП(ТаблицыОдногоДокумента[Счетчик].ОсновноеСредство);
		КонецЕсли;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьСтрока);
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		
		Если НЕ ТабДокумент.ПроверитьВывод(СтрокаСПодвалом)  Тогда
			ТабДокумент.Вывести(ОбластьПодвалСтраницы);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ОбластьШапка2);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьСтрока);
		
		ИтогСумма = ИтогСумма + ?(ТаблицыОдногоДокумента[Счетчик].СуммаПеремещения <> Null, ТаблицыОдногоДокумента[Счетчик].СуммаПеремещения, 0);
		ОбластьПодвал.Параметры.ИтогСумма = ИтогСумма;
		
		Нс = НС + 1;
		
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьПодвал);	
	
КонецПроцедуры

